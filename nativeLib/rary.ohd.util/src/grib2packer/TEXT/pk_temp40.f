      SUBROUTINE PK_TEMP40(KFILDO,IPACK,ND5,IS4,NS4,L3264B,
     1                     LOCN,IPOS,IER,*)
C
C        MARCH   2000   LAWRENCE   GSC/TDL   ORIGINAL CODING
C        JANUARY 2001   GLAHN      COMMENTS; IER=403 CHANGED TO 402
C
C        PURPOSE
C            PACKS TEMPLATE 4.0 INTO PRODUCT DEFINITION SECTION 
C            OF A GRIB2 MESSAGE.  TEMPLATE 4.0 IS FOR AN ANALYSIS
C            OR FORECAST AT A HORIZONTAL LEVEL OR IN A HORIZONTAL
C            LAYER AT A POINT IN TIME.  IT IS THE RESPONSIBILITY
C            OF THE CALLING ROUTINE TO PACK THE FIRST 9 OCTETS
C            IN SECTION 4.
C
C            NOTE THAT TEMPLATE 4.0 FORMS THE BASIS FOR MANY
C            OF THE OTHER PRODUCT DEFINITION TEMPLATES 
C            AVAILABLE TO BE PACKED IN SECTION 4 OF THE GRIB2
C            MESSAGE.
C
C        DATA SET USE
C           KFILDO - UNIT NUMBER FOR OUTPUT (PRINT) FILE. (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT)
C            IPACK(J) = THE ARRAY THAT HOLDS THE ACTUAL PACKED MESSAGE
C                       (J=1,ND5). (INPUT/OUTPUT)
C                 ND5 = THE SIZE OF THE ARRAY IPACK( ). (INPUT)
C              IS4(J) = CONTAINS THE PRODUCT DEFINITION INFORMATION 
C                       FOR TEMPLATE 4.0 (IN ELEMENTS 10 THROUGH 31)
C                       THAT WILL BE PACKED INTO IPACK( ) (J=1,NS4).
C                       (INPUT)
C                 NS4 = SIZE OF IS4( ). (INPUT) 
C              L3264B = THE INTEGER WORD LENGTH IN BITS OF THE MACHINE
C                       BEING USED. VALUES OF 32 AND 64 ARE
C                       ACCOMMODATED. (INPUT)
C                LOCN = THE WORD POSITION TO PLACE THE NEXT VALUE.
C                       (INPUT/OUTPUT)
C                IPOS = THE BIT POSITION IN LOCN TO START PLACING
C                       THE NEXT VALUE. (INPUT/OUTPUT)
C                 IER = RETURN STATUS CODE. (OUTPUT)
C                        0 = GOOD RETURN.
C                       1-4 = ERROR CODES GENERATED BY PKBG. SEE THE 
C                             DOCUMENTATION IN THE PKBG ROUTINE.
C                       402 = IS4( ) HAS NOT BEEN DIMENSIONED LARGE
C                             ENOUGH TO CONTAIN THE ENTIRE TEMPLATE. 
C                   * = ALTERNATE RETURN WHEN IER NE 0. 
C
C             LOCAL VARIABLES
C             MINSIZE = THE SMALLEST ALLOWABLE DIMENSION FOR IS4( ).
C                   N = L3264B = THE INTEGER WORD LENGTH IN BITS OF
C                       THE MACHINE BEING USED. VALUES OF 32 AND
C                       64 ARE ACCOMMODATED.
C
C        NON SYSTEM SUBROUTINES CALLED
C           PKBG
C
      PARAMETER(MINSIZE=31)
C
      DIMENSION IPACK(ND5),IS4(NS4)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/util/src/grib2packer/RCS/pk_temp40.f,v $
     . $',                                                             '
     .$Id: pk_temp40.f,v 1.1 2004/09/16 16:52:29 dsa Exp $
     . $' /
C    ===================================================================
C
C
      N=L3264B
      IER=0
C
C        CHECK THE DIMENSIONS OF IS4( ).
C
      IF(NS4.LT.MINSIZE)THEN
         IER=402
      ELSE
C
C           PACK THE PRODUCT DEFINITION TEMPLATE.
C        
C           PACK THE PARAMETER CATEGORY.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(10),8,N,IER,*900)
C
C           PACK THE PARAMETER NUMBER.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(11),8,N,IER,*900)
C
C           PACK THE TYPE OF GENERATING PROCESS.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(12),8,N,IER,*900)
C
C           PACK THE BACKGROUND GENERATING PROCESS IDENTIFIER.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(13),8,N,IER,*900)
C
C           PACK THE ANALYSIS OR FORECAST GENERATING PROCESS IDENTIFIER.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(14),8,N,IER,*900)
C
C           PACK THE HOURS OF OBSERVATIONAL DATA CUTOFF AFTER 
C           REFERENCE TIME.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(15),16,N,IER,*900)
C
C           PACK THE MINUTES OF OBSERVATIONAL DATA CUTOFF AFTER
C           REFERENCE TIME.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(17),8,N,IER,*900)
C
C           PACK THE INDICATOR OF UNIT OF TIME RANGE.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(18),8,N,IER,*900)
C
C           PACK THE FORECAST TIME IN UNITS DEFINED BY OCTET 19.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(19),32,N,IER,*900)
C
C           PACK THE TYPE OF FIRST FIXED SURFACE.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(23),8,N,IER,*900)
C
C           PACK THE SCALE FACTOR OF FIRST FIXED SURFACE.
         ISIGN=0
         IF(IS4(24).LT.0) ISIGN=1
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,ISIGN,1,N,IER,*900)
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,ABS(IS4(24)),7,N,IER,*900)
C
C           PACK THE SCALED VALUE OF FIRST FIXED SURFACE.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(25),32,N,IER,*900)
C
C           PACK THE TYPE OF SECOND FIXED SURFACE.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(29),8,N,IER,*900)
C
C           PACK THE SCALE FACTOR OF SECOND FIXED SURFACE.
         ISIGN=0
         IF(IS4(30).LT.0) ISIGN=1
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,ISIGN,1,N,IER,*900)
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,ABS(IS4(30)),7,N,IER,*900)
C
C           PACK THE SCALED VALUE OF SECOND FIXED SURFACE.
         CALL PKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(31),32,N,IER,*900)
      ENDIF
C
C       ERROR RETURN SECTION
 900  IF(IER.NE.0)RETURN 1 
C
      RETURN
      END
