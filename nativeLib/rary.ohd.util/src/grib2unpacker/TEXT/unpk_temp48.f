      SUBROUTINE UNPK_TEMP48(KFILDO,IPACK,ND5,IS4,NS4,L3264B,
     1                       LOCN,IPOS,IER,*)
C
C        MARCH     2000   LAWRENCE GSC/TDL    ORIGINAL CODING
C        JANUARY   2001   GLAHN    COMMENTS; CHANGED TO STANDARD 
C                                  RETURN SEQUENCE; CHANGED IER = 14
C                                  TO 402; CHECKED FOR SIZE OF IS4( )
C                                  FOR ALL TIME IS4(42) TIME RANGES
C        FEBRUARY  2001   GLAHN    CHECKED TEMPLATE NUMBER; COMMENTS
C        MAY       2002   GLAHN    CORRECTED ERROR IN CALL TO 
C                                  UNPK_TEMP40
C        SEPTEMBER 2002   GLAHN    CORRECTED COMMENT
C
C        PURPOSE
C            UNPACKS TEMPLATE 8, AN AVERAGE, ACCUMULATION, OR EXTREME
C            VALUE TEMPLATE, FROM THE PRODUCT DEFINITION SECTION OF A 
C            GRIB2 MESSAGE.  IT IS THE RESPONSIBILITY OF THE CALLING 
C            ROUTINE TO UNPACK THE FIRST 9 OCTETS IN SECTION 4.
C
C        DATA SET USE
C           KFILDO - UNIT NUMBER FOR OUTPUT (PRINT) FILE. (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER FOR OUTPUT (PRINT) FILE. (INPUT)
C            IPACK(J) = THE ARRAY THAT HOLDS THE ACTUAL PACKED MESSAGE
C                       (J=1,ND5). (INPUT/OUTPUT)
C                 ND5 = THE SIZE OF THE ARRAY IPACK( ). (INPUT)
C              IS4(J) = THE PRODUCT DEFINITION INFORMATION FOR
C                       TEMPLATE 4.8 IS WRITTEN TO ELEMENTS 10 THROUGH
C                       55+12*(IS4(42)-1) AS IT IS UNPACKED FROM 
C                       IPACK( ) (J=1,NS4).  (INPUT/OUTPUT)
C                 NS4 = SIZE OF IS4( ). (INPUT)
C              L3264B = THE INTEGER WORD LENGTH IN BITS OF THE MACHINE
C                       BEING USED. VALUES OF 32 AND 64 ARE
C                       ACCOMMODATED. (INPUT)
C                LOCN = THE WORD POSITION FROM WHICH TO UNPACK THE
C                       NEXT VALUE. (INPUT/OUTPUT)
C                IPOS = THE BIT POSITION IN LOCN FROM WHICH TO START
C                       UNPACKING THE NEXT VALUE.  (INPUT/OUTPUT)
C                 IER = RETURN STATUS CODE. (OUTPUT)
C                         0 = GOOD RETURN.
C                       6-8 = ERROR CODES GENERATED BY UNPKBG. SEE THE 
C                             DOCUMENTATION IN THE UNPKBG ROUTINE.
C                       402 = IS4( ) HAS NOT BEEN DIMENSIONED LARGE
C                             ENOUGH TO CONTAIN THE ENTIRE TEMPLATE. 
C                       403 = NOT THE CORRECT TEMPLATE. 
C                   * = ALTERNATE RETURN WHEN IER NE 0. 
C
C             LOCAL VARIABLES
C             MINSIZE = THE SMALLEST ALLOWABLE DIMENSION FOR IS4( ).
C                   N = L3264B = THE INTEGER WORD LENGTH IN BITS OF
C                       THE MACHINE BEING USED. VALUES OF 32 AND
C                       64 ARE ACCOMMODATED.
C
C        NON SYSTEM SUBROUTINES CALLED
C           UNPKBG, UNPK_TEMP40
C
      PARAMETER(MINSIZE=43)
C
      DIMENSION IPACK(ND5),IS4(NS4)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/util/src/grib2unpacker/RCS/unpk_temp48.f,v $
     . $',                                                             '
     .$Id: unpk_temp48.f,v 1.1 2004/09/16 16:51:50 dsa Exp $
     . $' /
C    ===================================================================
C
C
      N=L3264B
      IER=0
C
C        CHECK TO MAKE SURE THAT THIS IS TEMPLATE 4.8. 
C
      IF(IS4(8).NE.8)THEN
D        WRITE(KFILDO,10)IS4(8)
D10      FORMAT(/' TEMPLATE ',I4,' INDICATED BY IS4(8)'/
D    1           ' IS NOT CORRECT IN UNPK_TEMP48.'/)
         IER=403
         GO TO 900
      ENDIF
C
C        CHECK THE DIMENSIONS OF IS4( ).
C
      IF(NS4.LT.MINSIZE)THEN
D        WRITE(KFILDO,20)NS4,MINSIZE
D20      FORMAT(/' IS4( ) IS CURRENTLY DIMENSIONED TO CONTAIN'/
D    1           ' NS4=',I4,' ELEMENTS. THIS ARRAY MUST BE'/
D    2           ' DIMENSIONED TO AT LEAST ',I4,' ELEMENTS'/
D    3           ' TO CONTAIN ALL OF THE DATA IN PRODUCT'/
D    4           ' DEFINITION TEMPLATE 4.8.'/)
         IER=402
         GO TO 900
      ENDIF
C
C        SINCE THIS TEMPLATE SHARES THE SAME INFORMATION
C        AS TEMPLATE 4.0, CALL THE UNPK_TEMP40 ROUTINE
C        TO RETRIEVE OCTETS 10 THROUGH 34.
c
      CALL UNPK_TEMP40(KFILDO,IPACK,ND5,IS4,NS4,L3264B,
     1                 LOCN,IPOS,IER,*900)
C
C        UNPACK THE YEAR 
      CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(35),16,N,IER,*900)
C
C        UNPACK THE MONTH 
      CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(37),8,N,IER,*900)
C
C        UNPACK THE DAY 
      CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(38),8,N,IER,*900)
C
C        UNPACK THE HOUR 
      CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(39),8,N,IER,*900)
C
C        UNPACK THE MINUTE 
      CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(40),8,N,IER,*900)
C
C        UNPACK THE SECOND 
      CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(41),8,N,IER,*900)
C
C        UNPACK THE NUMBER OF TIME RANGE SPECIFICATIONS DESCRIBING
C        THE TIME INTERVALS USED TO CALCULATE THE STATISTICALLY
C        PROCESSED FIELD.
      CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(42),8,N,IER,*900)
C
C        PACK THE TOTAL NUMBER OF DATA VALUES MISSING IN
C        STATISTICAL PROCESS.
      CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(43),32,N,IER,*900)
C
C        CHECK TO MAKE SURE THAT IS4( ) WAS DIMENSIONED LARGE ENOUGH
C        TO CONTAIN THE DATA FOR ALL OF THE IS4(42) TIME RANGES.
C
      ISIZE=55+12*(IS4(42)-1)
      IF(NS4.LT.ISIZE)THEN
D        WRITE(KFILDO,19)NS4,ISIZE
D19      FORMAT(/' IS4( ) IS CURRENTLY DIMENSIONED TO CONTAIN'/
D    1           ' NS4=',I4,' ELEMENTS.  THIS ARRAY MUST BE'/
D    2           ' DIMENSIONED TO AT LEAST ',I4,' ELEMENTS'/
D    3           ' TO CONTAIN ALL OF THE TIME RANGES'/
D    4           ' IN PRODUCT DEFINITION TEMPLATE 4.8.'/)
         IER=402
         GO TO 900
      ENDIF
C
      DO 30 I=1,IS4(42)
         IC=12*(I-1)
C
C           UNPACK THE STATISTICAL PROCESS USED TO CALCULATE THE
C           PROCESSED FIELD FROM THE FIELD AT EACH TIME INCREMENT
C           DURING THE TIME RANGE.
         CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(47+IC),8,N,
     1             IER,*900)
C
C           UNPACK THE TYPE OF TIME INCREMENT BETWEEN SUCCESSIVE
C           FIELDS USED IN THE STATISTICAL PROCESS.
         CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(48+IC),8,N,
     1             IER,*900)
C
C           UNPACK THE INDICATOR OF UNIT OF TIME FOR TIME RANGE OVER
C           WHICH STATISTICAL PROCESSING IS DONE.
         CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(49+IC),8,N,
     1             IER,*900)
C
C           UNPACK THE LENGTH OF THE TIME RANGE OVER WHICH STATISTICAL
C           PROCESSING IS DONE, IN UNITS DEFINED BY THE PREVIOUS
C           OCTET.
         CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(50+IC),32,N,
     1             IER,*900)
C
C           UNPACK THE INDICATOR OF UNIT OF TIME FOR THE INCREMENT
C           BETWEEN THE SUCCESSIVE FIELDS USED.
         CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(54+IC),8,N,
     1             IER,*900)
C
C           UNPACK THE TIME INCREMENT BETWEEN SUCCESSIVE FIELDS,
C           IN UNITS DEFINED BY THE PREVIOUS OCTET.
         CALL UNPKBG(KFILDO,IPACK,ND5,LOCN,IPOS,IS4(55+IC),32,N,
     1             IER,*900)
C
 30   ENDDO
C
C       ERROR RETURN SECTION
C
 900  IF(IER.NE.0)RETURN 1
C
      RETURN
      END
