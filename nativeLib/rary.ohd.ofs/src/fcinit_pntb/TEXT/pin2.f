C MODULE PIN2
C-----------------------------------------------------------------------
C
      SUBROUTINE PIN2 (PO,LEFTP,IUSEP,CO,LEFTC,IUSEC)
C
C  THIS IS THE INPUT ROUTINE FOR THE UNIT HYDROGRAPH OPERATION.
C
C  THIS ROUTINE INPUTS ALL CARDS FOR THE OPERATION AND FILLS THE PO
C  ARRAY.
C
C  ROUTINE INITIALLY WRITTEN BY LARRY BRAZIL - HRL - 08/1979
C
      CHARACTER*4 METENG
      CHARACTER*8 CARRY
C
      DIMENSION PO(*),CO(*)
      DIMENSION ANAME(5),ROID(2),QID(2)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin2.f,v $
     . $',                                                             '
     .$Id: pin2.f,v 1.4 2002/02/11 19:00:56 dws Exp $
     . $' /
C    ===================================================================
C
C
      IF (ITRACE.GE.1) WRITE (IODBUG,*) 'ENTER PIN2'
C
      IBUG=0
      IF (IDBALL.GT.0) IBUG=1
      IF (NDEBUG.GT.0) THEN
         DO 10 I=1,NDEBUG
         IF (IDEBUG(I).EQ.2) THEN
            IBUG=1
            GO TO 20
            ENDIF
10       CONTINUE
         ENDIF
C
20    IPOUG=26
      IVER=1
      NOFILL=0
      SUM=0.0
      IUSEP=0
      IUSEC=0
      ICAR=0
      NCARD=0
C
C  READ INPUT CARD 1
      NCARD=NCARD+1
      READ (IN,30) ANAME,AREA,NV,CARRY,METENG,BASE
30    FORMAT (5A4,5X,F10.1,2X,I3,2X,A,1X,A,F10.3)
      LENGL=-1
      IF (METENG.EQ.'METR'.OR.METENG.EQ.' ') LENGL=0
      IF (METENG.EQ.'ENGL') LENGL=1
      IF (LENGL.EQ.-1) THEN
         WRITE (IPR,40) METENG
40    FORMAT ('0**WARNING** ENGLISH METRIC SWITCH (',A4,
     * ') MUST BE ''METR'', ''ENGL'' OR BLANK. METRIC IS ASSUMED.')
         CALL WARN
         LENGL=0
         ENDIF
C
      IF (LENGL.EQ.1) THEN
         ARENG=AREA
         XKM=((.3048*5280.)/1000.)**2
         AREA=ARENG*XKM
         ENDIF
C
C  READ INPUT CARD 2
      NCARD=NCARD+1
      READ (IN,50) ROID,ROTYPE,IDTR,QID,QTYPE,IDTQ
50    FORMAT (2X,2A4,1X,A4,3X,I2,2X,2A4,1X,A4,3X,I2)
C  CHECK TO SEE IF THE TIME SERIES EXIST AND CHECK DIMENSIONS
      IVALUE=1
      IMISS=0
      IDIMEN=1
      CALL CHEKTS (ROID,ROTYPE,IDTR,IDIMEN,'L   ',IMISS,IVALUE,IERR)
      CALL CHEKTS (QID,QTYPE,IDTQ,IDIMEN,'L3/T',IMISS,IVALUE,IERR)
      IF (IDTQ.EQ.0) GO TO 100
C  CHECK THAT Q TIME INTERVAL LE RUNOFF TIME INTERVAL
      IF (IDTR.LT.IDTQ) THEN
         WRITE (IPR,60) IDTR,IDTQ
60    FORMAT ('0**ERROR** TIME INTERVAL OF RUNOFF TIME SERIES (',
     * I2,' HOURS) IS LESS THAN THE TIME INTERVAL OF ',
     * 'DISCHARGE SERIES (',I2,').')
         CALL ERROR
         GO TO 80
         ENDIF
C  CHECK THAT THE TIME INTERVAL OF THE RUNOFF TIME SERIES IS AN EVEN
C  MULTIPLE OF THE TIME INTERVAL OF THE DISCHARGE SERIES
      IF ((IDTR/IDTQ)*IDTQ.NE.IDTR) THEN
         WRITE (IPR,70) IDTR,IDTQ
70    FORMAT ('0**ERROR** TIME INTERVAL OF RUNOFF TIME SERIES (',
     * I2,' HOURS) IS NOT AN EVEN MULTIPLE OF THE TIME INTERVAL OF ',
     * 'DISCHARGE SERIES (',I2,').')
         CALL ERROR
         ENDIF
80    LUG=NV*IDTQ
      IF (LUG.LT.IDTR) THEN
         WRITE (IPR,90) LUG,IDTR
90    FORMAT('0**ERROR** THE TOTAL LENGTH OF THE UNIT HYDROGRAPH (',
     * I2,' HOURS) IS LESS THAN THE TIME INTERVAL OF THE RUNOFF ',
     * 'TIME SERIES (',I2,').')
         CALL ERROR
         ENDIF
C
C  CHECK SPACE IN P ARRAY TO SEE IF UNIT HYDROGRAPH ORDINATES
C  CAN BE PUT IN DIRECTLY.
100   IF (IPOUG+NV.GT.LEFTP) THEN
         NOFILL=1
         WRITE (IPR,110) 'P',IPOUG+NV,LEFTP
110   FORMAT ('0**ERROR** NUMBER OF WORDS NEEDED IN THE ',A,
     * ' ARRAY (',I3,') EXCEEDS THE SPACE AVAILABLE (',I3,').')
         CALL ERROR
         ENDIF
C  CALCULATE RUNOFF CARRYOVER VALUES AND RESULTING COMPUTED Q'S
C   NCO = NUMBER OF Q'S ADDED AT BEGINNING OF EXECUTION
C   NRO = NUMBER OF RUNOFF CO VALUES
      NCO=NV-IDTR/IDTQ
      IF (NCO.LT.0) NCO=0
      NRO=(NV-1)/(IDTR/IDTQ)
      IF (NRO.GT.LEFTC) THEN
         NOFILL=1
         WRITE (IPR,110) 'C',NRO,LEFTC
         CALL ERROR
         ENDIF
C
      IF (NOFILL.EQ.1) GO TO 250
C
C  READ INPUT CARD 3
      NCARD=NCARD+1
      READ (IN,120,ERR=123) (PO(IPOUG+I),I=1,NV)
120   FORMAT (7F10.4)
      GO TO 127
123   WRITE (IPR,125) NCARD
125   FORMAT ('0**ERROR** READING VALUES FROM CARD ',I1,'.')
      CALL ERROR
C  COMPUTE AREA REPRESENTED BY UNIT HYDROGRAPH.
127   DO 130 J=1,NV
         SUM=SUM+PO(IPOUG+J)
130      CONTINUE
      PPD=24./IDTQ
      IF (LENGL.EQ.0) THEN
C     METRIC AREA COMPUTATIONS
         AREAH=SUM/PPD*86.4
         FD=ABS(AREA-AREAH)/AREAH
         IF (FD.GT.0.01) THEN
            WRITE (IPR,140) AREAH,'KM2',AREA,'KM2'
140   FORMAT ('0**WARNING** AREA REPRESENTED BY THE UNIT HYDROGRAPH (',
     *      F8.1,' ',A,') DIFFERS FROM THE USER SPECIFIED AREA (',
     *      F8.1,' ',A,')' /
     * 13X,'BY MORE THAN ONE PERCENT.')
            CALL WARN
            ENDIF
         ENDIF
      IF (LENGL.EQ.1) THEN
C     ENGLISH AREA COMPUTATIONS
         AREAH=SUM/PPD*.03719
         FD=ABS(ARENG-AREAH)/AREAH
         IF (FD.GT.0.01) THEN
            WRITE (IPR,140) AREAH,'MI2',ARENG,'MI2'
            CALL WARN
            ENDIF
         AREAH=AREAH*XKM
         ENDIF
C
C  READ INPUT CARD 4 IF NEEDED
      NCARD=NCARD+1
      IF (NRO.EQ.0) THEN
         WRITE (IPR,150)
150   FORMAT ('0**WARNING** THE SPECIFIED UNIT HYDROGRAPH WILL NOT ',
     * 'PROVIDE ANY CARRYOVER VALUES.')
         CALL WARN
         GO TO 180
         ENDIF
      IF (CARRY.EQ.'   CARRY') THEN
         ICAR=1
         READ (IN,120,ERR=155) (CO(I),I=1,NRO)
         GO TO 180
155      WRITE (IPR,125) NCARD
         CALL ERROR
         GO TO 180
         ENDIF
      DO 170 J=1,NRO
         CO(J)=0.0
170      CONTINUE
180   IUSEP=IPOUG+NV
      IUSEC=NRO
C
      IF (LENGL.EQ.1) THEN
C     CONVERT PO AND CO VALUES TO METRIC
         XCMS=(.3048)**3
         XMM=25.4
         XPO=XCMS/XMM
         DO 190 I=1,NV
            PO(IPOUG+I)=PO(IPOUG+I)*XPO
190         CONTINUE
         BASE=BASE*XCMS
         IF (NRO.GT.0) THEN
            DO 200 I=1,NRO
               CO(I)=CO(I)*XMM
200            CONTINUE
            ENDIF
         ENDIF
C
C  STORE INFORMATION IN PO ARRAY
      PO(1)=IVER+0.01
      DO 210 I=1,5
         PO(I+1)=ANAME(I)
210      CONTINUE
      PO(7)=AREA
      PO(8)=AREAH
      PO(9)=IUSEP+0.01
      PO(10)=NV+0.01
      PO(11)=ICAR+0.01
      PO(12)=1.0
      PO(13)=ROID(1)
      PO(14)=ROID(2)
      PO(15)=ROTYPE
      PO(16)=IDTR+0.01
      PO(17)=QID(1)
      PO(18)=QID(2)
      PO(19)=QTYPE
      PO(20)=IDTQ+0.01
      PO(21)=NRO+0.01
      PO(22)=IPOUG+1.01
      PO(23)=LENGL+0.01
      PO(24)=BASE
      DO 220 I=25,26
         PO(I)=0.01
220      CONTINUE
C  HYDROGRAPH ORDINATES STORED IN PO(IPOUG+1) TO PO(IPOUG+NV).
C
      IF (IBUG.EQ.1) THEN
         WRITE (IODBUG,*) 'IUSEP=',IUSEP,' IUSEC=',IUSEC
         WRITE (IODBUG,230) 'PO'
230   FORMAT (' CONTENTS OF THE ',A,' ARRAY:')
         WRITE (IODBUG,240) (PO(I),I=1,IUSEP)
240   FORMAT (' ',15F8.3)
         WRITE (IODBUG,230) 'CO'
         WRITE (IODBUG,240) (CO(I),I=1,IUSEC)
         ENDIF
C
250   IF (IBUG.EQ.1) THEN
         WRITE (IODBUG,*) 'NOFILL=',NOFILL,' PPD=',PPD,' SUM=',SUM
         ENDIF
C
      RETURN
C
C.......................................................................
C
C  DEFINITION OF SELECTED VARIABLES:
C     NOFILL=1 IF PO() AND CO() WERE NOT FILLED, OTHERWISE=0.
C     IVER = VERSION NUMBER FOR THIS OPERATION.
C     SUM = SUM OF THE UNIT HYDROGRAPH ORDINATES.
C     AREAH = AREA REPRESENTED BY UNIT HYDROGRAPH.
C.......................................................................
C
C  CONTENTS OF THE PO ARRAY - UNIT HYDROGRAPH OPERATION:
C
C     POSITION             CONTENTS
C        1     VERSION NUMBER FOR THE OPERATION
C        2-6   GENERAL NAME FOR AREA OR POINT
C        7     USER SPECIFIED AREA
C        8     AREA REPRESENTED BY UNIT HYDROGRAPH
C        9     NUMBER OF PO() VALUES
C        10    NUMBER OF HYDROGRAPH ORDINATES
C        11    INITIAL CARRYOVER SWITCH
C        12    FOR FUTURE USE(CURRENTLY SET TO 1.0)
C        13-14 RUNOFF TIME SERIES I.D.
C        15    RUNOFF TIME SERIES DATA TYPE
C        16    RUNOFF TIME SERIES TIME INTERVAL
C        17-18 DISCHARGE TIME SERIES I.D.
C        19    DISCHARGE TIME SERIES DATA TYPE
C        20    DISCHARGE TIME SERIES TIME INTERVAL
C        21    NUMBER OF CARRYOVER VALUES
C        22    POSITION IN PO() OF FIRST HYDROGRAPH ORDINATE: (27)
C        23    METRIC SWITCH (0=METRIC UNITS, 1=ENGLISH UNITS)
C        24    BASEFLOW CONSTANT
C        25-26 EXTRA SPACE FOR FUTURE OPTIONS(SET TO 0.01)
C        27-   UNIT HYDROGRAPH ORDINATES
C
C  MINIMUM SIZE OF THE PO ARRAY=IPOUG+1
C  MAXIMUM=DETERMINED BY NUMBER OF ORDINATES.
C.......................................................................
C
      END
