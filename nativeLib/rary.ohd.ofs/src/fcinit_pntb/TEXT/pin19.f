C MODULE PIN19
C-----------------------------------------------------------------------
C
      SUBROUTINE PIN19(PS,LEFTP,IUSEP,CS,LEFTC,IUSEC)
C
C  THIS IS THE INPUT SUBROUTINE FOR THE 'SNOW-17 ' OPERATION.
C  TEMPERATURE INDEX SNOW MODEL FIRST DESCRIBED IN NOAA TECH MEMO
C  NWS-HYDRO-17.
C
C     SUBROUTINE INITIALLY WRITTEN BY: ERIC ANDERSON - HRL - 5/1980
C
C      MODIFIED 4/00 BY V. KOREN TO ADD TWO MORE STATES:
C          SNOW DEPTH (SNDPT) AND TEMPERATURE (SNTMP)
C
C      MODIFIED 6/03 BY A. Vo TO ADD OBS SNOW DEPTH (OSNDPT) 
C
C      MODIFIED 11/05 BY ERIC ANDERSON TO CORRECT VARIOUS PROBLEMS
C
      CHARACTER*80 CARD
      REAL MFMAX,MFMIN,NMF,MBASE,NEGHS,LIQW
      DIMENSION PS(*),CS(*)
      DIMENSION SNAME(2),ANAME(5),UPS(2),UPDATE(2),PXID(2),RMID(2)
      DIMENSION TAID(2),OWEID(2),OSCID(2),SWEID(2),SSCID(2),RSID(2)
CVK  ADDED SNOW DEPTH TIME SERIES IDs
      DIMENSION SHID(2)
cav   observed snowdepth timeseries ids
      DIMENSION OHID(2)      
      DIMENSION PSID(2)
      DIMENSION ADC(11),SMFV(12),AE(2,14)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fprog'
CVK  ADDED TWO MORE STATES: SNDPT, SNTMP ------
      COMMON /SNCO19/ WE,NEGHS,LIQW,TINDEX,ACCMAX,SB,SBAESC,SBWS,
CVK     1STORGE,AEADJ,NEXLAG,EXLAG(7)
C     1STORGE,AEADJ,NEXLAG,EXLAG(7),SNDPT,SNTMP
CVK -------------------------------------------     
CEA  ADDED TAPREV AS A STATE VARIABLE
     1STORGE,AEADJ,NEXLAG,EXLAG(7),SNDPT,SNTMP,TAPREV
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin19.f,v $
     . $',                                                             '
     .$Id: pin19.f,v 1.8 2006/10/03 18:48:07 xfan Exp $
     . $' /
C    ===================================================================
C
      DATA SNAME/4HPIN1,4H9   /
      DATA ALL,FNONE,YES,SUMS,UPDATE,RDCO/4HALL ,4HNONE,3HYES,4HSUMS,
     1  4HUPDA,2HTE,4HRDCO/
      DATA BLANK/4H    /
      DATA DL,DLES,TEMP/4HL   ,4HDLES,4HTEMP/
      DATA AVSE,CMETR/4HAVSE,4HMETR/
C
C     TRACE LEVEL=1, DEBUG SWITCH=IBUG
      CALL FPRBUG(SNAME,1,19,IBUG)
C.......................................
C     INITIALIZE CONTROL VARIABLES
      IUSEP=0
      IUSEC=0
      NOFILL=0
      IVER=4
C*****************************************************************
C   VERSION 1 - BASIC SNOW MODEL WITHOUT DEPTH COMPUTATIONS
C   VERSION 2 - SNOW DEPTH COMPUTATIONS ADDED WITH 2 ADDITIONAL
C                 STATE VARIABLES - SNOW DEPTH AND TEMPERATURE
C   VERSION 3 - OBSERVED SNOW DEPTH TIME SERIES ADDED - A NEW 
C                 VERSION NUMBER SHOULD NOT HAVE BEEN USED FOR
C                 THIS PURPOSE - OBS. DEPTH WAS ADDED TO SNCO19
C                 COMMON BLOCK IN SOME ROUTINES, BUT WAS NEVER
C                 WRITTEN TO THE CARRYOVER FILES, THUS CARRYOVER
C                 REMAINED AS IN VERSION 2.
C   VERSION 4 - ADDITIONAL STATE VARIABLE ADDED - TAPREV.  THIS
C                 ALLOWS THE OPERATION TO PRODUCE EXACTLY THE SAME
C                 RESULTS NO MATTER WHICH CARRYOVER DATE IS USED
C                 TO START THE RUN - PREVIOUSLY THERE WOULD BE
C                 MINOR DIFFERENCES IN DEPTH AND TEMPERATURE
C                 COMPUTATIONS FOR DIFFERENT DATES.
C*****************************************************************
      LRM=0
      LPCTS=0
      LOWE=0
      LSWE=0
CVK
      LSSDPT=0
cav....
      LOSDPT=0
      LOSC=0
      LCOVER=0
      LSUMS=0
      LPM=35
      LADC=LPM+14
      LTAPM=0
      LAEC=0
      LUPPM=0
      LMFV=0
      NXPS=LADC+9
C.......................................
C     READ INPUT CARDS AND MAKE CHECKS.
C
C     INPUT CARD NUMBER 1
      READ(IN,900) ANAME,ELEV,ALAT,PRC,ODS,OSS,SS,UPS,RDCOS,AEC
  900 FORMAT(5A4,2F5.0,1X,A4,2X,A3,2X,A3,1X,A4,4X,A4,A2,1X,A4,1X,A4)
      IPRINT=2
      IF(PRC.EQ.FNONE)IPRINT=0
      IF(PRC.EQ.ALL)IPRINT=1
      IOBS=0
      IF(ODS.EQ.YES)IOBS=1
      ISIM=0
      IF(OSS.EQ.YES)ISIM=1
      IF(SS.NE.SUMS) GO TO 101
      LSUMS=NXPS
      NXPS=NXPS+7
  101 IUP=0
      IF((UPS(1).EQ.UPDATE(1)).AND.(UPS(2).EQ.UPDATE(2))) IUP=1
      IRDCO=0
      IF(RDCOS.EQ.RDCO) IRDCO=1
      IAEC=0
      IF (AEC.EQ.AVSE) IAEC=1
C.......................................
C     INPUT CARD NUMBER 2
      READ (IN,901) ITPX,PXID,PXTYPE,PXADJ,RMID,RMTYPE,PSID,PSTYPE
  901 FORMAT(3X,I2,2X,2A4,1X,A4,F10.0,7X,2A4,1X,A4,7X,2A4,1X,A4)
      IF(ITPX.LE.0) ITPX=6
      CALL CHEKTS(PXID,PXTYPE,ITPX,1,DL,0,1,IERR)
      IF((RMID(1).EQ.BLANK).AND.(RMID(2).EQ.BLANK)) GO TO 102
      LRM=NXPS
      NXPS=NXPS+3
      CALL CHEKTS(RMID,RMTYPE,ITPX,1,DL,0,1,IERR)
  102 IF((PSID(1).EQ.BLANK).AND.(PSID(2).EQ.BLANK)) GO TO 105
      LPCTS=NXPS
      NXPS=NXPS+3
      CALL CHEKTS(PSID,PSTYPE,ITPX,1,DLES,1,1,IERR)
C.......................................
C     INPUT CARD NUMBER 3
  105 READ(IN,902) TAID,TATYPE,IDT,TAELEV,TALMAX,TALMIN
  902 FORMAT(2X,2A4,1X,A4,3X,I2,F10.0,2F5.0)
      IF(IDT.LE.0) IDT=6
      CALL CHEKTS(TAID,TATYPE,IDT,1,TEMP,0,1,IERR)
      IF((IDT/ITPX)*ITPX.EQ.IDT) GO TO 106
      WRITE(IPR,903)IDT,ITPX
  903 FORMAT(1H0,10X,25H**ERROR** TIME INTERVAL (,I2,1X,
     148HHOURS) OF THE TEMPERATURE DATA IS NOT A MULTIPLE,/16X,
     222HOF THE TIME INTERVAL (,I2,1X,28HHOURS) OF THE PRECIPITATION.)
      CALL ERROR
  106 IF(TAELEV.EQ.0.0) GO TO 160
      IF(TAELEV.EQ.ELEV) GO TO 160
      LTAPM=NXPS
      NXPS=NXPS+3
      IF((TALMAX.GE.0.0).AND.(TALMIN.GE.0.0)) GO TO 160
      WRITE(IPR,904)
  904 FORMAT(1H0,10X,93H**WARNING** AT LEAST ONE OF THE LAPSE RATES ARE
     1NOT POSITIVE.  CHECK THAT VALUES ARE CORRECT.)
      CALL WARN
  160 IF (IAEC.EQ.0) GO TO 110
C.......................................
C     INPUT CARD NUMBER 3A
      READ(IN,913) NPTAE,EMIN,EMAX,AEUNT,RSID,RSTYPE
  913 FORMAT(3X,I2,2F10.0,1X,A4,2X,2A4,1X,A4)
      IF (NPTAE.LE.12) GO TO 161
      WRITE(IPR,914) NPTAE
  914 FORMAT(1H0,10X,'**ERROR** THE NUMBER OF AREA-ELEVATION CURVE POINT
     1S INPUT(',I2,') EXCEEDS 12.')
      CALL ERROR
      NPTAE=12
  161 IAEU=0
      IF (AEUNT.EQ.CMETR) IAEU=1
      LAEC=NXPS
      NXPS=NXPS+5+2*(NPTAE+2)
      CALL CHEKTS(RSID,RSTYPE,IDT,1,DL,0,1,IERR)
      IF (NPTAE.EQ.0) GO TO 165
C     INPUT CARD NUMBER 3B
      READ(IN,915,ERR=164) (AE(1,I+1),AE(2,I+1),I=1,NPTAE)
  915 FORMAT(4(F10.0,2X,F3.2))
      GO TO 165
  164 CALL FRDERR (IPR,'INPUT CARD NUMBER 3B',' ')
  165 AE(1,1)=EMIN
      AE(2,1)=0.0
      NPTAE=NPTAE+2
      AE(1,NPTAE)=EMAX
      AE(2,NPTAE)=1.0
      IER=0
      DO 167 I=2,NPTAE
      IF (AE(1,I).GE.AE(1,I-1)) GO TO 166
      IER=1
  166 IF (AE(2,I).GE.AE(2,I-1)) GO TO 167
      IER=1
  167 CONTINUE
      IF (IER.EQ.0) GO TO 110
      WRITE(IPR,916)
  916 FORMAT(1HO,10X,'**ERROR** ONE OR MORE VALUES IN THE AREA-ELEVATION
     1 CURVE ARE LESS THAN THE PRECEEDING VALUE.')
      CALL ERROR
  110 IF(IOBS.EQ.0) GO TO 120
C.......................................
C     INPUT CARD NUMBER 4
Cav  OBSERVED SNOW DEPTH INCLUDED - ONLY FOR DISPLAY PURPOSES
      READ(IN,905) OWEID,OWETYP,ITOWE,OSCID,OSCTYP,ITOSC,OHID,OHTYP,ITOH 
CVK  905 FORMAT(2X,2A4,1X,A4,3X,I2,12X,2A4,1X,A4,3X,I2)
  905 FORMAT(2X,2A4,1X,A4,3X,I2,12X,2A4,1X,A4,3X,I2,2X,2A4,1X,A4,3X,I2)      
      IF((OWEID(1).EQ.BLANK).AND.(OWEID(2).EQ.BLANK)) GO TO 111
      LOWE=NXPS
      NXPS=NXPS+4
      CALL CHEKTS(OWEID,OWETYP,ITOWE,1,DL,1,1,IERR)
      IF((ITOWE/IDT)*IDT.EQ.ITOWE) GO TO 111
      WRITE(IPR,906)OWEID,OWETYP,ITOWE,IDT
  906 FORMAT(1H0,10X,49H**ERROR** THE TIME INTERVAL OF TIME SERIES (I.D.
     1=,2A4,2X,5HTYPE=,A4,2X,I2,1X,6HHOURS),/16X,40HIS NOT A MULTIPLE OF
     2 THE TIME INTERVAL (,I2,1X,31HHOURS) OF THE TEMPERATURE DATA.)
      CALL ERROR
cav  111 IF((OSCID(1).EQ.BLANK).AND.(OSCID(2).EQ.BLANK)) GO TO 120
  111 IF((OSCID(1).EQ.BLANK).AND.(OSCID(2).EQ.BLANK)) GO TO 123
      LOSC=NXPS
      NXPS=NXPS+4
      CALL CHEKTS(OSCID,OSCTYP,ITOSC,1,DLES,1,1,IERR)
      IF((ITOSC/IDT)*IDT.EQ.ITOSC) GO TO 123
      WRITE(IPR,906)OSCID,OSCTYP,ITOSC,IDT
      CALL ERROR
  123 IF((OHID(1).EQ.BLANK).AND.(OHID(2).EQ.BLANK)) GO TO  120         
      LOSDPT=NXPS      
      NXPS=NXPS+4 
      CALL CHEKTS(OHID,OHTYP,ITOH,1,DL,1,1,IERR)
      IF((ITOH/IDT)*IDT.EQ.ITOH) GO TO 120
      WRITE(IPR,906) OHID,OHTYP,ITOH,IDT
      CALL ERROR
  120 IF(ISIM.EQ.0) GO TO 125
C.......................................
C     INPUT CARD NUMBER 5
C
CVK  ADD SIMULATED SNOW DEPTH TIME SERIES
CVK      READ(IN,905)SWEID,SWETYP,ITSWE,SSCID,SSCTYP,ITSSC
      READ(IN,905)SWEID,SWETYP,ITSWE,SSCID,SSCTYP,ITSSC,SHID,SHTYP,ITSH
      IF((SWEID(1).EQ.BLANK).AND.(SWEID(2).EQ.BLANK)) GO TO 121
      LSWE=NXPS
      NXPS=NXPS+4      
      CALL CHEKTS(SWEID,SWETYP,ITSWE,1,DL,0,1,IERR)
      IF((ITSWE/IDT)*IDT.EQ.ITSWE) GO TO 121
      WRITE(IPR,906) SWEID,SWETYP,ITSWE,IDT
      CALL ERROR
CVK
CVK  121 IF((SSCID(1).EQ.BLANK).AND.(SSCID(2).EQ.BLANK)) GO TO 125
  121 IF((SSCID(1).EQ.BLANK).AND.(SSCID(2).EQ.BLANK)) GO TO 162
      LCOVER=NXPS
      NXPS=NXPS+4
      CALL CHEKTS(SSCID,SSCTYP,ITSSC,1,DLES,0,1,IERR)
CVK
CVK      IF((ITSSC/IDT)*IDT.EQ.ITSSC) GO TO 125
      IF((ITSSC/IDT)*IDT.EQ.ITSSC) GO TO 162
      WRITE(IPR,906) SSCID,SSCTYP,ITSSC,IDT
      CALL ERROR
CVK  NEW TIME SERIES ADDED ----------------------------------
  162 IF((SHID(1).EQ.BLANK).AND.(SHID(2).EQ.BLANK)) GO TO 125
      LSSDPT=NXPS
      NXPS=NXPS+4
      CALL CHEKTS(SHID,SHTYP,ITSH,1,DL,0,1,IERR)
      IF((ITSH/IDT)*IDT.EQ.ITSH) GO TO 125
      WRITE(IPR,906) SHID,SHTYP,ITSH,IDT
      CALL ERROR
CVK ---------------------------------------------------------        
C     SET-UP SPACE FOR UPDATING PARAMETERS.
  125 IF(MAINUM.LT.3) GO TO 126
      IF(IUP.EQ.0) GO TO 130
      IF((LOWE.EQ.0).AND.(LOSC.EQ.0)) GO TO 130
      NUP=2
      GO TO 127
  126 NUP=7
  127 LUPPM=NXPS
      NXPS=NXPS+NUP
C.......................................
C     INPUT CARD NUMBER 6.
  130 READ (IN,'(A)') CARD
      READ (CARD,907,ERR=228) SCF,MFMAX,MFMIN,UADJ,SI,MV
  907 FORMAT(5F5.0,4X,I1)
      GO TO 229
228   CALL FRDERR (IPR,' ',CARD)
C     INPUT CARD NUMBER 6A IF NEEDED
  229 IF(MV.EQ.0) GOTO 133
      READ (IN,'(A)') CARD
      READ (CARD,908,ERR=230) SMFV
  908 FORMAT(12F5.0)
      GO TO 232
230   CALL FRDERR (IPR,' ',CARD)
232   LMFV=NXPS
      NXPS=NXPS+12
      DO 134 I=1,12
      IF (SMFV(I).LT.0.0) SMFV(I)=0.0
      IF (SMFV(I).GT.1.0) SMFV(I)=1.0
  134 CONTINUE
C.......................................
C     INPUT CARD NUMBER 7.
  133 READ (IN,'(A)') CARD
      READ (CARD,908,ERR=330) NMF,TIPM,MBASE,PXTEMP,PLWHC,DAYGM,WETOL,
     * SCTOL
      GO TO 331
330   CALL FRDERR (IPR,' ',CARD)
CEA  LOWER LIMIT CHANGED FOR TIPM PARAMETER TO 0.01.
CEA 331   IF(TIPM.LT.0.1) TIPM=0.1
331   IF(TIPM.LT.0.01) TIPM=0.01
      IF(TIPM.GT.1.0) TIPM=1.0
      IF(PLWHC.GT.1.0) PLWHC=PLWHC*0.01
CEA  MAKE SURE THAT PLWHC IS EQUAL OR SMALLER THAN 1.0 MINUS THE
CEA    MAXIMUM ALLOWED DENSITY (CURRENTLY 0.6 IN SNDEPTH19)
      IF (PLWHC.GT.0.4) PLWHC=0.4
C.......................................
C     INPUT CARD NUMBER 8.
      READ (IN,'(A)') CARD
      READ (CARD,908,ERR=430) (ADC(I),I=2,10)
      GO TO 431
430   CALL FRDERR (IPR,' ',CARD)
431   ADC(1)=0.05
      ADC(11)=1.0
      IER=0
      DO 131 I=2,10
      IF (ADC(I).GE.ADC(I-1)) GO TO 131
      IER=1
  131 CONTINUE
      IF (IER.EQ.0) GO TO 132
      WRITE (IPR,912)
  912 FORMAT (1H0,10X,93H**ERROR** ONE OR MORE VALUES OF THE AREAL DEPLE
     1TION CURVE ARE LESS THAN THE PRECEEDING VALUE.)
      CALL ERROR
C.......................................
C     CARRYOVER SECTION
  132 NEXLAG=5/ITPX+2
      IF(IRDCO.EQ.0) GO TO 135
C.......................................
C     INPUT CARD NUMBER 9.
CVK  ADDED TWO MORE STATES: SNDPT, SNTMP
CVK      READ(IN,907) WE,NEGHS,LIQW,TINDEX,ACCMAX,ICO
      READ (IN,'(A)') CARD
      READ (CARD,917,ERR=226) WE,NEGHS,LIQW,TINDEX,ACCMAX,ICO,
CEA  ADDED TAPREV AS A STATE VARIABLE
C     1  SNDPT,SNTMP
     1  SNDPT,SNTMP,TAPREV
C  917 FORMAT(5F5.0,4X,I1,2F5.0)
  917 FORMAT(5F5.0,4X,I1,3F5.0)
      GO TO 227
226   CALL FRDERR (IPR,' ',CARD)
227   IF(ICO.EQ.0) GO TO 135
      IF(ICO.EQ.0) GO TO 135
C.......................................
C     INPUT CARD NUMBER 10.
      READ (IN,'(A)') CARD
      READ (CARD,908,ERR=530) SB,SBAESC,SBWS,STORGE,AEADJ,
     * (EXLAG(I),I=1,NEXLAG)
      GO TO 531
530   CALL FRDERR (IPR,' ',CARD)
531   IRDCO=2
C
C     CHECK CARRYOVER VALUES.
  135 CALL CKCO19(IRDCO,PLWHC,SI,ADC,ITPX)
C.......................................
C     STORAGE REQUIREMENTS KNOWN CHECK SPACE IN P AND C ARRAYS
      NPS=NXPS-1
CVK  ADDED TWO MORE STATES
CVK      NCS=10+NEXLAG
CEA  ADDED TAPREV AS AN ADDITIONAL STATE
CEA      NCS=12+NEXLAG
      NCS=13+NEXLAG
      CALL CHECKP(NPS,LEFTP,IERR)
      IF(IERR.EQ.1) NOFILL=1
      CALL CHECKC(NCS,LEFTC,IERR)
      IF(IERR.EQ.1) NOFILL=1
      IF(NOFILL.EQ.1) RETURN
C.......................................
C     SPACE AVAILABLE - STORE ALL VALUES.
C
C     STORE PARAMETER VALUES IN PS().
      PS(1)=IVER+0.01
      DO 140 I=1,5
  140 PS(I+1)=ANAME(I)
      PS(7)=PXID(1)
      PS(8)=PXID(2)
      PS(9)=PXTYPE
      PS(10)=ITPX+0.01
      PS(11)=TAID(1)
      PS(12)=TAID(2)
      PS(13)=TATYPE
      PS(14)=IDT+0.01
      PS(15)=IRDCO+0.01
      PS(16)=NPS+0.01
      PS(17)=LRM+0.01
      PS(18)=LPCTS+0.01
      PS(19)=LOWE+0.01
      PS(20)=LSWE+0.01
      PS(21)=LOSC+0.01
      PS(22)=LCOVER+0.01
      PS(23)=LSUMS+0.01
      PS(24)=IPRINT+0.01
      PS(25)=LPM+0.01
      PS(26)=LADC+0.01
      PS(27)=LTAPM+0.01
      PS(28)=LUPPM+0.01
      PS(29)=LMFV+0.01
      PS(30)=LAEC+0.01
CVK  SIMULATED SNOW DEPTH TIME SERIES INFORMATION LOCATION
CVK      PS(31)=0.01
      PS(31)=LSSDPT+0.01
Cav  OBSERVED SNOW DEPTH TIME SERIES INFORMATION LOCATION
Cav      PS(32)=0.01
      PS(32)=LOSDPT+0.01
      PS(33)=0.01
      PS(34)=0.01
      PS(LPM)=PXADJ
      PS(LPM+1)=ELEV
      PS(LPM+2)=SCF
      PS(LPM+3)=MFMAX
      PS(LPM+4)=MFMIN
      PS(LPM+5)=UADJ
      PS(LPM+6)=SI
      PS(LPM+7)=NMF
      PS(LPM+8)=TIPM
      PS(LPM+9)=MBASE
      PS(LPM+10)=PXTEMP
      PS(LPM+11)=PLWHC
      PS(LPM+12)=DAYGM
      PS(LPM+13)=ALAT
      DO 141 I=2,10
      J=LADC+I-2
  141 PS(J)=ADC(I)
      IF(LMFV.EQ.0) GOTO 139
      DO 138 I=1,12
       J=LMFV+I-1
  138 PS(J)=SMFV(I)
  139 IF (LRM.EQ.0) GO TO 151
      PS(LRM)=RMID(1)
      PS(LRM+1)=RMID(2)
      PS(LRM+2)=RMTYPE
  151 IF(LPCTS.EQ.0) GO TO 142
      PS(LPCTS)=PSID(1)
      PS(LPCTS+1)=PSID(2)
      PS(LPCTS+2)=PSTYPE
  142 IF(LOWE.EQ.0) GO TO 143
      PS(LOWE)=OWEID(1)
      PS(LOWE+1)=OWEID(2)
      PS(LOWE+2)=OWETYP
      PS(LOWE+3)=ITOWE+0.01
  143 IF(LSWE.EQ.0) GO TO 144
      PS(LSWE)=SWEID(1)
      PS(LSWE+1)=SWEID(2)
      PS(LSWE+2)=SWETYP
      PS(LSWE+3)=ITSWE+0.01
cav  144 IF(LOSC.EQ.0) GO TO 145
  144 IF(LOSC.EQ.0) GO TO 170
      PS(LOSC)=OSCID(1)
      PS(LOSC+1)=OSCID(2)
      PS(LOSC+2)=OSCTYP
      PS(LOSC+3)=ITOSC+0.01
cav added ...for obs snow depth 
  170 IF(LOSDPT.EQ.0) GO TO 145
      PS(LOSDPT)=OHID(1)
      PS(LOSDPT+1)=OHID(2)
      PS(LOSDPT+2)=OHTYP
      PS(LOSDPT+3)=ITOH+0.01
CVK
CVK  145 IF(LCOVER.EQ.0) GO TO 146
  145 IF(LCOVER.EQ.0) GO TO 163
      PS(LCOVER)=SSCID(1)
      PS(LCOVER+1)=SSCID(2)
      PS(LCOVER+2)=SSCTYP
      PS(LCOVER+3)=ITSSC+0.01
CVK ------------------------------
  163 IF(LSSDPT.EQ.0) GO TO 146
      PS(LSSDPT)=SHID(1)
      PS(LSSDPT+1)=SHID(2)
      PS(LSSDPT+2)=SHTYP
      PS(LSSDPT+3)=ITSH+0.01
CVK ------------------------------
  146 IF(LSUMS.EQ.0) GO TO 148
      DO 147 I=1,7
  147 PS(LSUMS+I-1)=0.0
  148 IF(LTAPM.EQ.0) GO TO 150
      PS(LTAPM)=TAELEV
      PS(LTAPM+1)=TALMAX
      PS(LTAPM+2)=TALMIN
  150 IF (LAEC.EQ.0) GO TO 155
      PS(LAEC)=NPTAE+0.01
      PS(LAEC+1)=IAEU+0.01
      PS(LAEC+2)=RSID(1)
      PS(LAEC+3)=RSID(2)
      PS(LAEC+4)=RSTYPE
      CONV=1.0
      IF (IAEU.EQ.0) CONV=0.3048
      DO 152 I=1,NPTAE
      J=LAEC+5+(I-1)*2
      PS(J)=AE(1,I)*CONV
      PS(J+1)=AE(2,I)
  152 CONTINUE
  155 IF(LUPPM.EQ.0) GO TO 159
      PS(LUPPM)=WETOL
      PS(LUPPM+1)=SCTOL
      IF(NUP.EQ.2) GO TO 159
C     THE NEXT 2 POSITIONS MAY NOT BE ZERO IN ALL EXISTING FILES.
C         PREVIOUSLY HELD THE CARROLL PROCEDURE PARAMETERS.
      PS(LUPPM+2)=0.01
      PS(LUPPM+3)=0.01
      PS(LUPPM+4)=1.0
      PS(LUPPM+5)=1.0
      PS(LUPPM+6)=1.0
C     ALL PARAMETER VALUES STORED
  159 IUSEP=NPS
C.......................................
C     STORE CARRYOVER VALUES IN CS()
      CALL CSAV19(CS,IVER)
      IUSEC=NCS
C.......................................
C     CHECK FOR DEBUG OUTPUT
      IF(IBUG.EQ.0) GO TO 199
C     DEBUG OUTPUT.
      WRITE(IODBUG,909)
  909 FORMAT(1H0,43HSNOW-17 DEBUG--CONTENTS OF PS AND CS ARRAYS)
      WRITE(IODBUG,910) (PS(I),I=1,IUSEP)
  910 FORMAT(1H0,15F8.3)
      WRITE(IODBUG,911) (PS(I),I=1,IUSEP)
  911 FORMAT(1H0,15(4X,A4))
      WRITE(IODBUG,910) (CS(I),I=1,IUSEC)
C.......................................
  199 CONTINUE
      RETURN
      END