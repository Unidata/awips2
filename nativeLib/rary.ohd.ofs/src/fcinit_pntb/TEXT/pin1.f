C MODULE PIN1
C-----------------------------------------------------------------------
C
      SUBROUTINE PIN1(PL,LEFTP,IUSEP,CL,LEFTC,IUSEC)
C.......................................
C     THIS IS THE INPUT SUBROUTINE FOR THE SACRAMENTO SOIL-
C        MOISTURE ACCOUNTING OPERATION.  THIS SUBROUTINE
C        INPUTS ALL CARDS FOR THE OPERATION AND FILLS THE
C        PL ARRAY AND PLACES INITIAL VALUES OF THE
C        STATE VARIABLES INTO THE CL ARRAY.
C.......................................
C     SUBROUTINE INITIALLY WRITTEN BY. . .
C            ERIC ANDERSON - HRL     MARCH 1979     VERSION 1
C.......................................
      CHARACTER*80 CARD
      DIMENSION PL(*),CL(*)
      DIMENSION ANAME(5),PXID(2),ROID(2),SCID(2),ROCID(2),SMID(2),
     1PEID(2),NDAYS(12),MOPR(14),SNAME(2),CI(6),RSUM(7)
      REAL LZTWM,LZFSM,LZFPM,LZSK,LZPK,LZTWC,LZFSC,LZFPC
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fprog'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin1.f,v $
     . $',                                                             '
     .$Id: pin1.f,v 1.2 2002/02/11 18:59:55 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA DL,DLES/4HL   ,4HDLES/
      DATA PRST/4HPRST/
      DATA BLANK,SUMS,ROCL,SMZC/4H    ,4HSUMS,4HROCL,4HSMZC/
      DATA FRZE/4HFRZE/
      DATA SNAME/4HPIN1,4H    /
      DATA NDAYS/31,28,31,30,31,30,31,31,30,31,30,31/
C.......................................
C     CHECK TRACE LEVEL -- TRACE LEVEL FOR THIS SUBROUTINE=1.
C     CHECK TO SEE IF DEBUG OUTPUT IS NEEDED FOR THIS OPERATION.
      CALL FPRBUG (SNAME,1,1,IBUG)
C.......................................
C     INITIALIZE VARIABLES
      IUSEP=0
      IUSEC=0
      NOFILL=0
      IPLPM=31
      IPLET=IPLPM+20
      IPLX=IPLET+24
      NFPM=0
      JDT=24
      NXCO=0
      IVER=1
C.......................................
C     READ INPUT CARDS AND MAKE CHECKS
C
C     INPUT CARD NO. 1.
      READ (IN,901) ANAME,IDT,PXID,PXTYPE,ROID,ROTYPE
  901 FORMAT (5A4,8X,I2,2X,2A4,1X,A4,7X,2A4,1X,A4)
C     CHECK COMPUTATIONAL TIME INTERVAL
      IF (((24/IDT)*IDT).EQ.24) GO TO 110
      WRITE (IPR,902) IDT
  902 FORMAT (1H0,10X,24H**ERROR** TIME INTERVAL=,I2,5X,
     158HONLY INTERVALS OF 1,2,3,4,6,8,12 AND 24 HOURS ARE ALLOWED.)
      CALL ERROR
C     CHECK TO SEE IF TIME SERIES EXIST AND CHECK DIMENSIONS.
  110 CALL CHEKTS(PXID,PXTYPE,IDT,1,DL,0,1,IERR)
      CALL CHEKTS(ROID,ROTYPE,IDT,1,DL,1,1,IERR)
C
C     INPUT CARD NO. 2.
      READ (IN,906) SCID,SCTYPE,ITSC,ROCID,SMID,STORE,PROT,ITROC,ITSM,
     1FRZES
  906 FORMAT (2X,2A4,1X,A4,3X,I2,2X,2A4,2X,2A4,1X,A4,1X,A4,3X,I2,3X,I2,
     11X,A4)
      ROCTYP=ROCL
      SMTYPE=SMZC
      ISC=0
      IF ((SCID(1).EQ.BLANK).AND.(SCID(2).EQ.BLANK)) GO TO 115
      ISC=IPLX
      NXCO=NXCO+1
      ISCO=NXCO
      IPLX=IPLX+5
      CALL CHEKTS(SCID,SCTYPE,ITSC,1,DLES,0,1,IERR)
      IF ((ITSC/IDT)*IDT.EQ.ITSC) GO TO 115
      WRITE(IPR,904) SCID,SCTYPE,ITSC,IDT
  904 FORMAT (1H0,10X,49H**ERROR** THE TIME INTERVAL OF TIME SERIES (I.D
     1.=,2A4,2X,5HTYPE=,A4,2X,I2,1X,6HHOURS),/16X,
     239HIS NOT A MUTIPLE OF THE TIME INTERVAL (,I2,1X,
     329HHOURS) OF THE RAIN+MELT DATA.)
      CALL ERROR
  115 IROC=0
      IF ((ROCID(1).EQ.BLANK).AND.(ROCID(2).EQ.BLANK)) GO TO 117
      IROC=IPLX
      IROCO=NXCO+1
      NXCO=NXCO+7
      IF (ITROC.EQ.0) ITROC=24
      IPLX=IPLX+5
      CALL CHEKTS(ROCID,ROCTYP,ITROC,1,DL,1,7,IERR)
      IF ((ITROC/IDT)*IDT.EQ.ITROC) GO TO 117
      WRITE(IPR,904) ROCID,ROCTYP,ITROC,IDT
      CALL ERROR
  117 ISM=0
      IF ((SMID(1).EQ.BLANK).AND.(SMID(2).EQ.BLANK)) GO TO 118
      ISM=IPLX
      IF (ITSM.EQ.0) ITSM=24
      IPLX=IPLX+4
      CALL CHEKTS(SMID,SMTYPE,ITSM,1,DL,1,5,IERR)
      IF((ITSM/IDT)*IDT.EQ.ITSM) GO TO 118
      WRITE(IPR,904) SMID,SMTYPE,ITSM,IDT
      CALL ERROR
  118 ISUM=0
      IF (STORE.NE.SUMS) GO TO 119
      ISUM=IPLX
      IPLX=IPLX+16
  119 IPRINT=0
      IF (PROT.NE.PRST) GO TO 111
      IPRINT=1
  111 IFRZE=0
      IF (FRZES.NE.FRZE) GO TO 120
      IFRZE=1
C
C     INPUT CARD NO. 3
  120 READ (IN,908) PXADJ,PEADJ,UZTWM,UZFWM,UZK,PCTIM,ADIMP,RIVA,
     1IOPTET,EFC
  908 FORMAT (20X,2F5.2,2F5.0,4F5.2,I5,F5.2)
      IF (IOPTET.GT.0) IOPTET=1
      IF ((ISC.EQ.0).OR.(PXADJ.EQ.1.0)) GO TO 131
      WRITE (IPR,918)
  918 FORMAT (1H0,10X,84H**WARNING** NOTE THAT PXADJ MAY HAVE ALREADY BE
     1EN APPLIED BY A SNOW MODEL OPERATION.)
      CALL WARN
  131 ICHGE=0
C     PARAMETER CHECKS -- TO ELIMINATE IMPOSSIBLE VALUES.
      IF (PCTIM.LE.1.0) GO TO 121
      PCTIM=1.0
      ICHGE=ICHGE+1
  121 IF (ADIMP.LE.(1.0-PCTIM)) GO TO 122
      ADIMP=1.0-PCTIM
      ICHGE=ICHGE+1
  122 IF (RIVA.LE.1.0) GO TO 123
      RIVA=1.0
      ICHGE=ICHGE+1
  123 IF (EFC.LE.1.0) GO TO 125
      EFC=1.0
      ICHGE=ICHGE+1
C
C     INPUT CARD NO. 4
  125 READ (IN,909) ZPERC,REXP,LZTWM,LZFSM,LZFPM,LZSK,LZPK,PFREE,RSERV,
     1SIDE
  909 FORMAT (20X,2F5.1,3F5.0,5F5.2)
C     INSURE THAT CAPACITIES ARE NOT EQUAL TO 0.0
      IF (UZTWM.LT.0.1) UZTWM=0.1
      IF (UZFWM.LT.0.1) UZFWM=0.1
      IF (LZTWM.LT.0.1) LZTWM=0.1
      IF (LZFSM.LT.0.1) LZFSM=0.1
      IF (LZFPM.LT.0.1) LZFPM=0.1
C     PARAMETER CHECKS -- TO ELIMINATE IMPOSSIBLE VALUES.
      IF (UZK.LE.1.0) GO TO 126
      UZK=1.0
      ICHGE=ICHGE+1
  126 IF (LZSK.LE.1.0) GO TO 127
      LZSK=1.0
      ICHGE=ICHGE+1
  127 IF (LZPK.LE.1.0) GO TO 128
      LZPK=1.0
      ICHGE=ICHGE+1
  128 IF (PFREE.LE.1.0) GO TO 124
      PFREE=1.0
      ICHGE=ICHGE+1
  124 IF (RSERV.LE.1.0) GO TO 129
      RSERV=1.0
      ICHGE=ICHGE+1
  129 IF (ICHGE.EQ.0) GO TO 130
      WRITE (IPR,910) ICHGE
  910 FORMAT (1H0,10X,  11H**WARNING**,1X,I2,1X,69HPARAMETERS WERE CHANG
     1ED BECAUSE THEY WERE ASSIGNED IMPOSSIBLE VALUES.)
      CALL WARN
C
C     CHECK SPACE IN P ARRAY TO SEE IF ET-DEMAND OR
C        PE-ADJUSTMENT VALUES CAN BE PUT IN DIRECTLY.
  130 NOET=0
      NEEDP=IPLET+23
      CALL CHECKP(NEEDP,LEFTP,IERR)
      IF (IERR.EQ.0) GO TO 135
      NOET=1
      READ (IN,911) PEID,PETYPE
      GO TO 140
C
C     INPUT CARD NO. 5
  135 J=IPLET+11
      READ (IN,911) PEID,PETYPE,(PL(I),I=IPLET,J)
  911 FORMAT (2X,2A4,1X,A4,5X,12F4.2)
      IET=0
      IF ((PEID(1).EQ.BLANK).AND.(PEID(2).EQ.BLANK)) GO TO 136
      IET=IPLX
      NXCO=NXCO+1
      IETCO=NXCO
      IPLX=IPLX+4
      CALL CHEKTS(PEID,PETYPE,JDT,1,DL,0,1,IERR)
C     COMPUTE DAILY INCREMENT IN DEMAND CURVE OR
C        PE-ADJUSTMENT CURVE.
  136 DO 138 I=2,13
      ND=NDAYS(I-1)
      M=IPLET+I-1
      K=M-1
      IF (I.EQ.13) M=IPLET
      L=K+12
      PL(L)=(PL(M)-PL(K))/ND
  138 CONTINUE
C
C     INPUT CARD NO. 6
  140 READ (IN,'(A)') CARD
      READ (CARD,913,ERR=241) UZTWC,UZFWC,LZTWC,LZFSC,LZFPC,ADIMC,IRXC
  913 FORMAT (20X,6F5.0,4X,I1)
      GO TO 242
241   CALL FRDERR (IPR,' ',CARD)
C     CHECK STATE VARIABLES FOR ILLEGITIMATE VALUES.
242   CI(1)=UZTWC
      CI(2)=UZFWC
      CI(3)=LZTWC
      CI(4)=LZFSC
      CI(5)=LZFPC
      CI(6)=ADIMC
      L=0
      IF (UZTWC.LE.UZTWM) GO TO 20
      UZTWC=UZTWM
      L=1
   20 IF (UZFWC.LE.UZFWM) GO TO 21
      UZFWC=UZFWM
      L=1
   21 IF (LZTWC.LE.LZTWM) GO TO 22
      LZTWC=LZTWM
      L=1
   22 IF (LZFSC.LE.LZFSM) GO TO 23
      LZFSC=LZFSM
      L=1
   23 IF (LZFPC.LE.LZFPM) GO TO 24
      LZFPC=LZFPM
      L=1
   24 IF (ADIMC.LE.(UZTWM+LZTWM)) GO TO 25
      ADIMC=UZTWM+LZTWM
      L=1
   25 IF (ADIMC.GE.UZTWC) GO TO 26
      ADIMC=UZTWC
      L=1
   26 IF (L.EQ.0) GO TO 141
      WRITE (IPR,905)
  905 FORMAT (1H0,10X,11H**WARNING**)
      WRITE (IPR,907)
  907 FORMAT (1H ,10X,50HINITIAL STATE VARIABLES CONTAIN IMPOSSIBLE VALU
     1ES.,5X,5HUZTWC,2X,5HUZFWC,2X,5HLZTWC,2X,5HLZFSC,2X,5HLZFPC,
     22X,5HADIMC)
      WRITE (IPR,912) CI
  912 FORMAT (1H ,41X,19HINITIAL VALUES WERE,3X,6F7.0)
      WRITE (IPR,914) UZTWC,UZFWC,LZTWC,LZFSC,LZFPC,ADIMC
  914 FORMAT (1H ,50X,10HCHANGED TO,3X,6F7.0)
      CALL WARN
  141 IF(NXCO.EQ.0) GO TO 144
      IF(IRXC.EQ.1) GO TO 142
      DO 143 I=1,7
  143 RSUM(I)=0.0
      PPE=0.0
      PSC=0.0
      GO TO 144
C
C     READ CARD 6A
  142 READ (IN,'(A)') CARD
      READ (CARD,915,ERR=244) RSUM,PPE,PSC
  915 FORMAT(7F5.0,5X,2F5.0)
      GO TO 245
244   CALL FRDERR (IPR,' ',CARD)
245   DO 146 I=1,7
      IF (RSUM(I).LT.0.0) RSUM(I)=0.0
  146 CONTINUE
      IF(PPE.LT.0.0) PPE=0.0
      IF (PSC.LT.0.0) PSC=0.0
C
C     CHECK IF CARD NO. 7 IS NEEDED.
  144 IF ((IPRINT.EQ.0).OR.(MAINUM.LT.3)) GO TO 145
      IPRINT=IPLX
      IPLX=IPLX+15
      ICKMO=IPRINT+1
C     INPUT CARD NO. 7
      READ (IN,903) MOPR
  903 FORMAT (14I5)
C
C     CHECK IF FROZEN GROUND CARDS NEEDED.
  145 IF (IFRZE.EQ.0) GO TO 149
      CALL FRDFG1(IDT,NFPM,ISC,ITSC,NFCO,NXFCO)
      IFRZE=IPLX
      IPLX=IPLX+NFPM
  149 IF (NOET.EQ.0) GO TO 150
      NOFILL=1
      GO TO 151
C.......................................
C     STORAGE REQUIREMENTS NOW KNOWN - CHECK SPACE
C        AVAILABLE IN P AND C ARRAYS.
  150 NEEDP=IPLX-1
      CALL CHECKP(NEEDP,LEFTP,IERR)
      IF (IERR.EQ.1) NOFILL=1
  151 NEEDC=NXCO+6
      IF (IFRZE.GT.0) NEEDC=NEEDC+NFCO+NXFCO
      CALL CHECKC(NEEDC,LEFTC,IERR)
      IF (IERR.EQ.1) NOFILL=1
C.......................................
C     STORE INFORMATION IN PL AND CL ARRAYS.
      IF (NOFILL.EQ.1) GO TO 190
      PL(1)=IVER+0.01
      PL(2)=IDT+0.01
      DO 153 I=1,5
  153 PL(I+2)=ANAME(I)
      PL(8)=PXID(1)
      PL(9)=PXID(2)
      PL(10)=PXTYPE
      PL(11)=ROID(1)
      PL(12)=ROID(2)
      PL(13)=ROTYPE
      PL(14)=IET+0.01
      PL(15)=ISC+0.01
      PL(16)=IROC+0.01
      PL(17)=ISM+0.01
      PL(18)=ISUM+0.01
      PL(19)=IPRINT+0.01
      PL(20)=IPLPM+0.01
      PL(21)=IPLET+0.01
      PL(22)=1.0
      PL(23)=NXCO+0.01
      PL(24)=IFRZE+0.01
      J=IPLX-1
      PL(25)=J+0.01
      PL(26)=0.01
      PL(27)=0.01
      PL(28)=0.01
      PL(29)=0.01
      PL(30)=0.01
C
C     STORE PARAMETER VALUES.
      PL(IPLPM)=PXADJ
      PL(IPLPM+1)=PEADJ
      PL(IPLPM+2)=UZTWM
      PL(IPLPM+3)=UZFWM
      PL(IPLPM+4)=UZK
      PL(IPLPM+5)=PCTIM
      PL(IPLPM+6)=ADIMP
      PL(IPLPM+7)=RIVA
      PL(IPLPM+8)=ZPERC
      PL(IPLPM+9)=REXP
      PL(IPLPM+10)=LZTWM
      PL(IPLPM+11)=LZFSM
      PL(IPLPM+12)=LZFPM
      PL(IPLPM+13)=LZSK
      PL(IPLPM+14)=LZPK
      PL(IPLPM+15)=PFREE
      PL(IPLPM+16)=RSERV
      PL(IPLPM+17)=SIDE
      PL(IPLPM+18)=IOPTET+0.01
      PL(IPLPM+19)=EFC
C     ET-CURVE HAS BEEN STORED ALREADY.
C
C     STORE OPTIONAL VALUES.
      IF (ISC.EQ.0) GO TO 154
      PL(ISC)=SCID(1)
      PL(ISC+1)=SCID(2)
      PL(ISC+2)=SCTYPE
      PL(ISC+3)=ITSC+0.01
      PL(ISC+4)=ISCO+0.01
  154 IF (IROC.EQ.0) GO TO 155
      PL(IROC)=ROCID(1)
      PL(IROC+1)=ROCID(2)
      PL(IROC+2)=ROCTYP
      PL(IROC+3)=ITROC+0.01
      PL(IROC+4)=IROCO+0.01
  155 IF (ISM.EQ.0) GO TO 156
      PL(ISM)=SMID(1)
      PL(ISM+1)=SMID(2)
      PL(ISM+2)=SMTYPE
      PL(ISM+3)=ITSM+0.01
  156 IF (ISUM.EQ.0) GO TO 157
      DO 158 I=1,16
      PL(ISUM+I-1)=0.0
  158 CONTINUE
  157 IF (IET.EQ.0) GO TO 160
      PL(IET)=PEID(1)
      PL(IET+1)=PEID(2)
      PL(IET+2)=PETYPE
      PL(IET+3)=IETCO+0.01
  160 IF (IPRINT.LE.1) GO TO 162
      PL(IPRINT)=ICKMO+0.01
      DO 161 I=1,14
      J=ICKMO+I-1
  161 PL(J)=MOPR(I)+0.01
  162 IF (IFRZE.EQ.0) GO TO 170
      IC=NEEDC-NFCO-NXFCO+1
      CALL FSTFG1(PL(IFRZE),CL(IC),NFCO,NXFCO)
C
C     STORE INITIAL STORAGES.
  170 CL(1)=UZTWC
      CL(2)=UZFWC
      CL(3)=LZTWC
      CL(4)=LZFSC
      CL(5)=LZFPC
      CL(6)=ADIMC
      IF(NXCO.EQ.0) GO TO 175
      IF(IROC.EQ.0) GO TO 172
      J=IROCO+6-1
      DO 171 I=1,7
  171 CL(J+I)=RSUM(I)
  172 IF(IET.EQ.0) GO TO 173
      CL(IETCO+6)=PPE
  173 IF(ISC.EQ.0) GO TO 175
      CL(ISCO+6)=PSC
  175 IUSEP=NEEDP
      IUSEC=NEEDC
      IF (IBUG.EQ.0) GO TO 190
C.......................................
C     DEBUG OUTPUT - PRINT PL( ) AND CL( ).
      WRITE (IODBUG,919) IUSEP,IUSEC
  919 FORMAT(1H0,29HCONTENTS OF PL AND CL ARRAYS.,5X,
     121HNUMBER OF VALUES--PL=,I3,2X,3HCL=,I2)
      WRITE (IODBUG,916) (PL(I),I=1,IUSEP)
  916 FORMAT (1H0,15F8.3)
      WRITE (IODBUG,916) (CL(I),I=1,IUSEC)
  190 IF (IBUG.EQ.0) GO TO 199
      WRITE (IODBUG,917) NOFILL,IPLX,NOET,ISC,IROC,ISM,ISUM,IET
  917 FORMAT (1H0,7HNOFILL=,I1,2X,5HIPLX=,I3,2X,5HNOET=,I1,2X,4HISC=,I3,
     12X,5HIROC=,I3,2X,4HISM=,I3,2X,5HISUM=,I3,2X,4HIET=,I3)
C.......................................
C     DEFINITION OF SELECTED VARIABLES.
C        IET=LOCATION OF PE TIME SERIES INFRO. IN PL( )
C        IPLET=FIRST STORAGE POSITION FOR ET-CURVE IN PL( )
C        IPLPM=FIRST STORAGE POSITION FOR PARAMETERS IN PL( )
C        IPLX=NEXT OPEN STORAGE POSITION IN PL( )
C        IROC=LOCATION OF RUNOFF COMPONENT T.S. INFRO. IN PL( )
C        ISC=LOCATION OF AREAL EXTENT OF SNOW T.S. INFRO. IN PL( )
C        ISM=LOCATION OF SOIL MOISTURE STORAGE T.S. INFRO. IN PL( )
C        ISUM=LOCATION OF SUMS IN PL( )
C        IVER=VERSION NUMBER FOR THIS OPERATION-NOT FOR THE PROGRAM
C        NOET=1 IF ET-CURVE NOT ABLE TO BE STORED, OTHERWISE=0
C        NOFILL=1 IF PL( ) AND CL( ) WERE NOT FILLED, OTHERWISE=0
C.......................................
C
C     CONTENTS OF THE PL ARRAY. -SACRAMENTO SMA OPERATION.
C
C     POSITION                    CONTENTS
C        1         VERSION NUMBER FOR THE OPERATION - INTEGER
C        2         COMPUTATIONAL TIME INTERVAL IN HOURS
C        3-7       GENERAL NAME FOR AREA OR POINT.
C        8-9       RAIN + MELT TIME SERIES I.D.
C        10        RAIN + MELT TIME SERIES DATA TYPE
C        11-12     RUNOFF TIME SERIES I.D.
C        13        RUNOFF TIME SERIES DATA TYPE
C        14        LOCATION IN PL OF PE TIME SERIES INFRO.,
C                        =0 IF NO PE DATA
C        15        LOCATION IN PL OF AREAL EXTENT OF SNOW TIME SERIES
C                        =0 IF NO AREAL EXTENT OF SNOW TIME SERIES
C        16        LOCATION IN PL OF RUNOFF COMPONENT TIME SERIES
C                        INFRO., =0 IF NO RUNOFF COMPONENT TIME SERIES
C                        PRODUCED.
C        17        LOCATION IN PL OF SOIL MOISTURE CONTENT TIME SERIES
C                        INFRO., =0 IF NO MOISTURE CONTENT TIME SERIES
C                        PRODUCED.
C        18        LOCATION IN PL OF WATER BALANCE COMPONENTS, =0 IF
C                        WATER BALANCE COMPONENTS NOT SAVED.
C        19        CONTROL FOR PRINTING DETAILED OUTPUT.
C                    IF =0 -- DO NOT PRINT
C                    IF GREATER THAN ZERO --
C                       OPERATIONAL PROGRAMS--PRINT DETAILED OUTPUT.
C                       CALIBRATION PROGRAMS--LOCATION IN PL OF
C                            INFORMATION ON WHEN TO PRINT DETAILED
C                            OUTPUT.
C        20        LOCATION IN PL OF PARAMETER VALUES
C        21        LOCATION IN PL OF SEASONAL ET-CURVE
C        22        PRECIPITATION MULTIPYING FACTOR - APPLIED DURING THE
C                        COMPUTATIONAL PERIOD ONLY.  INITIALLY = 1.0
C        23        NUMBER OF EXTRA CARRYOVER VALUES.
C        24        LOCATION IN PL OF FROZEN GROUND INFORMATION.
C                        =0 IF NOT CONSIDERED.
C        25        NUMBER OF VALUES IN THE PL ARRAY.
C        26-30     RESERVED FOR FUTURE USE.
C
C     ORDER FOR PARAMETER VALUES IS...
C                      1. PXADJ     2. PEADJ     3. UZTWM     4. UZFWM
C                      5. UZK       6. PCTIM     7. ADIMP     8. RIVA
C                      9. ZPERC    10. REXP     11. LZTWM    12. LZFSM
C                     13. LZFPM    14. LZSK     15. LZPK     16. PFREE
C                     17. RSERV    18. SIDE     19. DIURNAL ET VARIATION
C                     20. EFC
C
C     SEASONAL ET CURVE INFORMATION IS...
C        1-12      SEASONAL ET-DEMAND OR PE-ADJUSTMENT CURVE (JAN-DEC)
C        13-24     DAILY INCREMENTS IN SEASONAL ET-DEMAND OR
C                        PE-ADJUSTMENT CURVE (FIRST VALUE IS FOR MID
C                        JAN. TO MID FEB.)
C
C     STARTING LOCATION OF OPTIONAL VALUES ARE GIVEN ABOVE, THE
C     CONTENTS OF THESE OPTIONAL BLOCKS ARE AS FOLLOWS. . .
C
C     A. PE TIME SERIES INFRO. - 2 WORDS FOR I.D., 1 WORD FOR DATA TYPE,
C              1 WORD FOR CARRYOVER LOCATION.
C     B. AREAL EXTENT OF SNOW TIME SERIES INFRO. - 2 WORDS FOR I.D.,
C              1 WORD FOR DATA TYPE, 1 WORD FOR TIME INTERVAL,
C              1 WORD FOR CARRYOVER LOCATION.
C     C. RUNOFF COMPONENT TIME SERIES INFRO. - 2 WORDS FOR I.D.,
C              1 WORD FOR DATA TYPE, 1 WORD FOR TIME INTERVAL,
C              1 WORD FOR CARRYOVER LOCATION.
C     D. SOIL MOISTURE CONTENT TIME SERIES INFRO. - 2 WORDS FOR I.D.,
C              1 WORD FOR DATA TYPE, 1 WORD FOR TIME INTERVAL.
C     E. WATER BALANCE COMPONENTS -- FOR CALIBRATION, ORDER IS...
C           1. PRECIP.     2. RUNOFF         3. RECHARGE    4. ACTUAL-ET
C           5. RESIDUAL    6. IMPERIVOUS     7. DIRECT      8. SURFACE
C           9. INTERFLOW  10. SUPP. GW      11. PRIMARY    12. ET-DEMAND
C          13. ET-UZ      14. ET-LZ         15. ET-ADIMP   16. ET-RIVA
C        FOR OPERATIONAL PROGRAMS VALUES STORED FOR BOTH THE
C        OBSERVED AND FUTURE PERIODS.  ORDER IS...
C           1. PRECIP      2. RUNOFF         3. SURFACE     4. DIR+IMPV
C           5. INTERFLOW   6. BASEFLOW       7. ET-DEMAND   8. ACTUAL-ET
C     F. MONTHS TO PRINT DETAILED OUTPUT.
C        UP TO SEVEN MONTHS CAN BE PRINTED.
C                  1 WORD FOR MONTH BEING CHECKED.
C                  2 WORDS PER MONTH - (MOPR,IYPR)
C     G. FROZEN GROUND INFORMATION.
C              LISTED IN SUBROUTINE FSTFG1.
C.......................................
  199 RETURN
      END
