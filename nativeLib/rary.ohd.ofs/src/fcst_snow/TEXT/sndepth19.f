      SUBROUTINE SNDEPTH19(WE,SLIQ,SFALL,SMELT,SGMLOS,SRFRZ,
     +                   TA,DTA,IDT,SNDPT,SNDEN,SNTMP,IBUG)
C
C....................................................................
C
C  SUBROUTINE CALCULATES SNOW DEPTH AND SNOW TEMPERATURE FOR THE 
C    COMPUTATIONAL TIME PERIOD
C
C     INITIALLY WRITTEN BY VICTOR KOREN IN APRIL 2000.
C
C     MODIFIED 11/05 BY E. ANDERSON
C       - VARIABLE NAMES CHANGED TO BE CONSISTENT FROM ONE ROUTINE TO
C         THE NEXT.
C       - SIGNIFICANT CHANGES TO COMPUTATIONAL SEQUENCE
C
C....................................................................
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_snow/RCS/sndepth19.f,v $
     . $',                                                             '
     .$Id: sndepth19.f,v 1.2 2006/10/03 19:32:54 hsu Exp $
     . $' /
C    ===================================================================
C
C     INPUT VARIABLES
C **  WE     - WATER EQUIVALENT, MM
C **  SLIQ   - LIQUID WATER CONTENT, MM
C **  SFALL  - NEW SNOWFALL, MM
C **  SMELT  - SURFACE MELT, MM
C **  SGMLOS - GROUND MELT, MM
C **  SRFRZ  - REFROZEN MELT/RAIN WATER, MM
C **  TA     - AIR TEMPERATURE, C
C **  DTA    - AIR TEMPERATURE CHANGE FROM THE PREVIOUS TIME PERIOD  
C **  IDT    - TIME STEP OF THE COMPUTATIONAL PERIOD, HR
C **  SNDPT  - SNOW DEPTH, CM
C **  SNDEN  - SNOW DENSITY, G/CM3 (ICE PORTION OF SNOW COVER)
C **  SNTMP  - AVERAGE SNOW TEMPERATURE, C
C **  IBUG   - DEBUG FLAG
C
C     OTHER VARIABLES
C **  TSNEW  - TEMP OF NEW SNOW, C
C **  DPTNEW - DEPTH OF NEW SNOW, CM
C **  DENNEW - DENSITY OF NEW SNOW, G/CM3
C **  DCM    - DENSITY INCREASE DUE TO MELT METAPHORISM
C                (REFREEZING OF LIQUID WATER), MULTIPLYING FACTOR
C **  WE1    - VALUE OF WE AT THE START OF THE PERIOD, MM
C **  WEX    - WATER EQUIVALENT OF EXISTING SNOW AT END OF PERIOD, MM
C **  DPTOLD - DEPTH OF OLD SNOW AFTER ANY MELT, CM
C **  TSNDPT - TOTAL DEPTH (OLD AND NEW SNOW), CM
C **  TSNDEN - TOTAL DENSITY (OLD AND NEW SNOW), G/CM3
C **  SFALLX - SNOWFALL MINUS MELT, MM
C **  PLOSS  - TOTAL MELT DURING THE PERIOD - SURFACE MELT PLUS
C                GROUNDMELT, MM
C
C  VALUES INPUT TO THE SUBROUTINE
C    WE, SLIQ - VALUE AT THE END OF THE CURRENT COMPUTATIONAL 
C       TIME PERIOD
C    SFALL, SMELT, SGMLOS, SRFRZ - AMOUNTS THAT OCCUR DURING THE PERIOD
C    TA - PERIOD AVERAGE VALUE
C    SNDPT, SNDEN, SNTMP - VALUE AT THE BEGINNING OF THE PERIOD
C  VALUES OUTPUT
C    SNDPT, SNDEN, SNTMP - VALUE AT THE END OF THE PERIOD
C
C  CHECK FOR DEBUG
      IF (IBUG.GT.0) WRITE(IODBUG,900) SNDPT,SNDEN,SNTMP,TA,DTA,
     1  WE,SLIQ,SFALL,SMELT,SGMLOS,SRFRZ
  900 FORMAT(1H ,2X,23HSNDEPTH19 INPUT--SNDPT=,F7.2,7H SNDEN=,F6.3,
     1  7H SNTMP=,F6.2,4H TA=,F6.2,5H DTA=,F6.2,4H WE=,F8.2,
     2  6H SLIQ=,F6.2,/10X,7H SFALL=,F7.3,7H SMELT=,F7.3,
     3  8H SGMLOS=,F7.3,7H SRFRZ=,F7.3)
      DT=IDT
C    ASSUME TEMPERATURE OF ANY NEW SNOW = AVG. AIR TEMPERATURE
C      AS FAR AS COMPUTING THE DENSITY OF NEW SNOW - ACTUAL
C      TEMPERATURE OF NEW SNOW CANNOT EXCEED 0.0 DEGC.
      DPTNEW=0.0
      DENNEW=0.0
      TSNEW=TA
      IF (SNDPT.GT.0.0) GO TO 100
C********************************************************************
C********************************************************************
C    NEW SNOW COVER - NONE EXISTED AT START OF PERIOD
C      KNOWN - WE.GT.0.0 (IF NOT, ROUTINE WOULDN'T BE CALLED)
C            - SFALL.GT.0 (SINCE SNOW EXISTS AND THERE WAS NONE
C                AT THE START OF THE PERIOD)
C     COMPUTE NET SNOWFALL DURING THE PERIOD
C        - SNOWFALL MINUS ANY SURFACE AND GROUND MELT.
C        - NET SNOWFALL POSITIVE OR NO SNOW WOULD EXIST
      SFALLX=SFALL-SMELT-SGMLOS
C     COMPUTE DEPTH AND DENSITY OF NET SNOWFALL
      CALL SNEW(TSNEW,SFALLX,DPTNEW,DENNEW)
C     ACCOUNT FOR ANY REFREEZE - INCREASES DENSITY - RECOMPUTE DEPTH
      IF (SRFRZ.GT.0.0) THEN
        DCM=(SFALLX+SRFRZ)/SFALLX
        DENNEW=DCM*DENNEW
        DPTNEW=(0.1*WE)/DENNEW
      ENDIF
C     SET TOTAL SNOW COVER VALUES AT END OF PERIOD.
      SNDPT=DPTNEW
      SNDEN=DENNEW
      SNTMP=TSNEW
      IF (SNTMP.GT.0.0) SNTMP=0.0
C   CHECK FOR DEBUG
      IF (IBUG.GT.0) WRITE(IODBUG,901) SFALLX,SNDPT,SNDEN,SNTMP
  901 FORMAT(1H ,2X,29HNEW SNOW ONLY OUTPUT--SFALLX=,F7.3,
     1  7H SNDPT=,F7.2,7H SNDEN=,F5.3,7H SNTMP=,F6.2)
      RETURN
C********************************************************************
C********************************************************************
C    SNOW COVER EXISTED AT START OF PERIOD
C     COMPUTE CHANGE IN DENSITY AND DEPTH OF EXISTING COVER
C     INCORPORATE EFFECT OF ANY NEW SNOW DURING THE PERIOD
C
C     COMPUTE NET MELT DURING PERIOD
  100 PLOSS=SMELT+SGMLOS
C     COMPUTE WE VALUE AT THE START OF THE PERIOD - EXISTING SNOW
      WE1=WE-SFALL+PLOSS-SRFRZ
C     CHECK FOR VARIOUS CASES
      IF (PLOSS.LT.WE1) THEN
C...............................................................
C     AMOUNT OF MELT IS LESS THAN EXISTING SNOW AT START OF PERIOD
C       ASSUME ALL MELT CAME FROM EXISTING SNOW - TYPICAL CASE
C     CHECK FOR ANY NEW SNOWFALL DURING PERIOD
C       IF ANY, GET DEPTH AND DENSITY
        IF (SFALL.GT.0.0) CALL SNEW(TSNEW,SFALL,DPTNEW,DENNEW)
C     COMPUTE WATER EQUIVALENT OF EXISTING SNOW AT END OF PERIOD
C       - PRIOR TO ANY REFREEZE
        WEX=WE1-PLOSS
C     COMPUTE DEPTH OF REMAINING OLD SNOW - ASSUME DENSITY OF
C       MELTED SNOW WAS THE SAME AS THE AVERAGE DENSITY OF THE
C       TOTAL OLD SNOW COVER
        DPTOLD=(0.1*WEX)/SNDEN
C     USE DENSITY OF COMBINED OLD AND NEW SNOW TO COMPUTE THE
C       THERMAL CONDUCTIVITY TO USE FOR CALCULATING THE CHANGE
C       IN THE TEMPERATURE OF THE SNOW COVER.
        TSNDPT=DPTOLD+DPTNEW
        TSNDEN=(DPTOLD*SNDEN+DPTNEW*DENNEW)/TSNDPT
C     COMPUTE NEW TEMPERATURE OF THE OLD SNOW
        CALL SNOWT(TSNDPT,TSNDEN,WE,SLIQ,TA,DTA,SNTMP,DPTNEW,IDT)
C     COMPUTE NEW DEPTH AND DENSITY OF OLD SNOW AFTER EFFECT OF
C       COMPACTION AND DESTRUCTIVE METAMORPHISM ARE TAKEN INTO
C       ACCOUNT.
        CALL SNOWPACK(WEX,DT,SNDPT,SNDEN,SLIQ,SNTMP,IBUG)
C     CHECK FOR DEBUG
        IF (IBUG.GT.0) WRITE(IODBUG,902) SNDPT,SNDEN,DPTNEW,DENNEW,
     1    SNTMP,TSNEW
  902   FORMAT(1H ,2X,26HMELT FROM OLD SNOW--SNDPT=,F7.2,7H SNDEN=,
     1    F5.3,8H DPTNEW=,F6.2,8H DENNEW=,F5.3,7H SNTMP=,F6.2,
     2    7H TSNEW=,F6.2)
      ELSE
        SFALLX=SFALL-PLOSS
        IF (SFALLX.GT.0.0) THEN
C...............................................................
C       MELT DURING THE PERIOD WAS GREATER THAN THE AMOUNT OF SNOW
C         AT THE START OF THE PERIOD AND NEW SNOWFALL EXCEEDS THE
C         AMOUNT OF MELT DURING THE PERIOD - ASSUME MELT COMES FROM
C         NEW SNOWFALL
C         GET DEPTH AND DENSITY OF NEW SNOW
          CALL SNEW(TSNEW,SFALLX,DPTNEW,DENNEW)
C         USE DENSITY OF COMBINED OLD AND NEW SNOW TO COMPUTE THE
C           THERMAL CONDUCTIVITY TO USE FOR CALCULATING THE CHANGE
C           IN THE TEMPERATURE OF THE SNOW COVER.
          TSNDPT=SNDPT+DPTNEW
          TSNDEN=(SNDPT*SNDEN+DPTNEW*DENNEW)/TSNDPT
C         COMPUTE NEW TEMPERATURE OF THE OLD SNOW
          CALL SNOWT(TSNDPT,TSNDEN,WE,SLIQ,TA,DTA,SNTMP,DPTNEW,IDT)
C         COMPUTE NEW DEPTH AND DENSITY OF OLD SNOW AFTER EFFECT OF
C           COMPACTION AND DESTRUCTIVE METAMORPHISM ARE TAKEN INTO
C           ACCOUNT.
          CALL SNOWPACK(WE1,DT,SNDPT,SNDEN,SLIQ,SNTMP,IBUG)
C         CHECK FOR DEBUG
          IF (IBUG.GT.0) WRITE(IODBUG,903) SNDPT,SNDEN,DPTNEW,DENNEW,
     1    SNTMP,TSNEW
  903   FORMAT(1H ,2X,26HMELT FROM NEW SNOW--SNDPT=,F7.2,7H SNDEN=,
     1    F5.3,8H DPTNEW=,F6.2,8H DENNEW=,F5.3,7H SNTMP=,F6.2,
     2    7H TSNEW=,F6.2)
        ELSE
C...............................................................
C       SFALLX IS .LE. 0.0, THUS MELT EXCEEDS BOTH EXISTING SNOW
C         AT THE START OF THE PERIOD AND THE NEW SNOWFALL, BUT NOT
C         THE TOTAL OF THE TWO QUANTITIES.  ASSUME ALL NEW SNOW
C         MELTS AND ONLY PART OF THE EXISTING PACK REMAINS.
          WEX=WE-SRFRZ
C         DEPTH OF EXISTING SNOW THAT REMAINS
          TSNDPT=(0.1*WEX)/SNDEN
C         COMPUTE NEW TEMPERATURE OF THE REMAINING OLD SNOW
          CALL SNOWT(TSNDPT,SNDEN,WE,SLIQ,TA,DTA,SNTMP,DPTNEW,IDT)
C         COMPUTE NEW DEPTH AND DENSITY OF REMAINING OLD SNOW AFTER
C           EFFECT OF COMPACTION AND DESTRUCTIVE METAMORPHISM ARE
C           TAKEN INTO ACCOUNT.
          CALL SNOWPACK(WEX,DT,SNDPT,SNDEN,SLIQ,SNTMP,IBUG)
C         CHECK FOR DEBUG
          IF (IBUG.GT.0) WRITE(IODBUG,904) SNDPT,SNDEN,DPTNEW,DENNEW,
     1    SNTMP,TSNEW
  904   FORMAT(1H ,2X,26HALL NEW SNOW MELTS--SNDPT=,F7.2,7H SNDEN=,
     1    F5.3,8H DPTNEW=,F6.2,8H DENNEW=,F5.3,7H SNTMP=,F6.2,
     2    7H TSNEW=,F6.2)
        ENDIF
      ENDIF
C...............................................................
C     IN ALL CASES WHEN SNOW EXISTED AT THE START OF THE PERIOD
C       COMPUTE NEW TOTAL DEPTH AND TEMPERATURE BY WEIGHING BOTH
C       THE OLD AND NEW SNOW.
      IF (TSNEW.GT.0.0) TSNEW=0.0
      SNTMP=(SNTMP*SNDPT+TSNEW*DPTNEW)/(SNDPT+DPTNEW)
      SNDPT=SNDPT+DPTNEW
      SNDEN=(0.1*(WE-SRFRZ))/SNDPT
C     COMPUTE EFFECT OF MELT METAMORPHISM - REFREEZING OF LIQUID
C       WATER WITHIN THE SNOW COVER.
      IF (SRFRZ.EQ.0.0) GO TO 110
C     COMPUTE INCREASE IN DENSITY DUE TO REFREEZING
      DCM=WE/(WE-SRFRZ)
      SNDEN=SNDEN*DCM
C     CHECK THAT DENSITY IS WITHIN THE RANGE OF 0.05 TO 0.60
  110 IF (SNDEN.GT.0.6) SNDEN=0.6
      IF (SNDEN.LT.0.05) SNDEN=0.05
C     COMPUTE SNOW DEPTH AT THE END OF THE PERIOD
      SNDPT=(0.1*WE)/SNDEN
C     CHECK FOR DEBUG
      IF (IBUG.GT.0) WRITE(IODBUG,905) SNDPT,SNDEN,SNTMP
  905 FORMAT(1H ,2X,24HSNDEPTH19 OUTPUT--SNDPT=,F7.2,7H SNDEN=,F5.3,
     1  7H SNTMP=,F6.2)
C
C***************************************************************
CEA  PREVIOUS VERSION OF THE SUBROUTINE FOLLOWS:
CEA      SUBROUTINE SNDEPTH19(WE,SLIQ,SFALL,SGSLOS,SRFRZ,
CEA     +                   TA,DTA,IDT,SNDPT,SNDEN,SNTMP)
C **  WE     - WATER EQUIVALENT, MM
C **  SLIQ   - LIQUID WATER CONTENT, MM
C **  DFALL  - NEW SNOWFALL, MM
C **  SGSLOS - GROUND SNOW MELT, MM
C **  SRFRZ  - REFROZEN MELT/RAIN WATER, MM
C **  TA     - AIR TEMPERATURE, C
C **  DTA    - AIR TEMPERATURE CHANGE FOR THE 6HR TIME INTERVAL  
C **  IDT    - TIME STEP, HR
C **  SH     - SNOW DEPTH, CM
C **  DS     - SNOW DENSITY, G/CM3
C **  TSNOW  - AVERAGE SNOW TEMPERATURE, CELSIUS
CEA      DT=IDT
C  ADJUST SNOW DENSITY DUE TO SNOWFALL
CEA      DHC=0.
CEA      SDN=0.0
CEA      TSNEW=TA
CEA      IF(DFALL .GT. 0.) THEN
C  CALCILATE NEW SNOW FALL DEPTH/DENSITY
CEA       CALL SNEW(TA,DFALL,DHC,SDN)
CEA       CALL SNOWT(DHC,SDN,DFALL,0.,TA,DTA,TSNEW,0.)
CEA       CALL SNOWPACK(DFALL,DT,DHC,SDN,0.,0.,0.,TSNEW)
CEA      ENDIF
CEA      IF(SH .GT. 0.0001) THEN
C  CALCULATE OLD SNOW COMPACTION/METAMORPHISM
CEA       SHN=DHC+SH
CEA       DSN=(SDN*DHC+DS*SH)/SHN
CEA       CALL SNOWT(SHN,DSN,WE,SLIQ,TA,DTA,TSNOW,DHC)
CEA       CALL SNOWPACK(WE,DT,SH,DS,SLIQ,DFALL,SRFRZ,TSNOW)
C  ACCOUNT FOR GROUND SNOW MELT
CEA       IF(SGSLOS .GT. 0.) SH=SH-0.1*SGSLOS/DS
CEA       IF(SH .LT. 0.) SH=0.0
C  COMBINE NEW SNOW FALL AND OLD SNOWPACK
CEA       TSNOW=(TSNOW*SH+TSNEW*DHC)/(SH+DHC)
CEA       SH=SH+DHC
CEA       DS=0.1*WE/SH
CEA      ELSE
C  THERE WAS NO SNOWPACK BEFORE THIS SNOW FALL
CEA       SH=DHC
CEA       DS=SDN
CEA       IF(SGSLOS .GT. 0.) SH=SH-0.1*SGSLOS/DS
CEA       IF(SH .LT. 0.) SH=0.0
CEA       TSNOW=TSNEW
CEA      ENDIF
C***************************************************************
      RETURN
      END
C
      SUBROUTINE SNOWT(SNDPT,SNDEN,WE,SLIQ,TA,DTA,SNTMP,DPTNEW,IDT)
C
C  SUBROUTINE CALCULATES AVERAGE SNOW TEMPERATURE
C      SNDPT   - SNOW DEPTH, CM (OLD AND NEW SNOW AFTER MELT)
C      SNDEN   - SNOW DENSITY, G/CM3 (OLD AND NEW SNOW AFTER MELT)
C      WE      - SNOW WATER EQUIVALENT, MM
C      SLIG    - TOTAL LIQUID WATER IN THE SNOW COVER, MM
C      TA      - AVERAGE AIR TEMPERATURE FOR THE CURRENT PERIOD, C
C      DTA     - CHANGE IN AIR TEMPERATURE FROM THE PREVIOUS PERIOD, C
C      SNTMP   - SNOW TEMPERATURE, C
C         INPUT - AT BEGINNING OF PERIOD (EXISTING SNOW)
C         OUTPUT - AT END OF PERIOD (OLD SNOW ONLY)
C      DPTNEW  - DEPTH OF NET NEW SNOWFALL DURING THE PERIOD, CM
C      IDT     - COMPUTATIONAL TIME INTERVAL, HR
C
C  SPECIFIC HEAT CAPACITIES OF ICE, WATER, AND AIR
C    UNITS ARE WATTS SEC/(M**3 C)
      PARAMETER (CICE=2.1E06,CH2O=4.2E06,CAIR=1E03)
C  HEAT WAVE LENGTH, IN SECONDS
      WAVEL=2.0*3600.*IDT      
C
C    CONVERT DEPTHS TO METERS
       SNDPTM=0.01*SNDPT
       DPTNWM=0.01*DPTNEW
C    GET TOTAL WATER EQUIVALENT AND DENSITY (SOLID PLUS LIQUID WATER)
       WETOT=WE+SLIQ
       DENTOT=0.1*WETOT/SNDPT 
C    COMPUTE THERMAL CONDUCTIVITY
       TC=0.0442*EXP(5.181*DENTOT)
C    COMPUTE FRACTION LIQUID
       FL=SLIQ/WETOT
C    COMPUTE EFFECTIVE SPECIFIC VOLUMETRIC HEAT CAPACITY
       C=CICE*SNDEN+CAIR*(1.-SNDEN-FL)+CH2O*FL
C    COMPUTE NEW SNOW COVER TEMPERATURE
       ALP=SQRT(3.14*C/(WAVEL*TC))
       IF(DPTNEW .GT. 0.) THEN
        SNTMP=SNTMP+DTA*((EXP(-ALP*DPTNWM)-EXP(-ALP*SNDPTM))/
     +        (ALP*(SNDPTM-DPTNWM)))
       ELSE
        SNTMP=SNTMP+DTA*((1.-EXP(-ALP*SNDPTM))/(ALP*SNDPTM))
       ENDIF 
      IF(SNTMP .GT. 0.) SNTMP=0.
      RETURN
      END
C
      SUBROUTINE SNEW (TSNEW,SFALL,DPTNEW,DENNEW )
C    
C  CALCULATES DEPTH AND DENSITY OF THE NEW SNOWFALL
C      TSNEW   - AIR TEMPERATURE, C (INPUT)
C      SFALL   - NET NEW SNOWFALL, MM (INPUT)
C      DPTNEW  - SNOW DEPTH, CM (OUTPUT)
C      DENNEW  - SNOW DENSITY (OUTPUT)
C
C ----------------------------------------------------------------------
C ***  CALCULATE NEW SNOWFALL DENSITY DEPENDING ON TEMPERATURE
C ***  EQUATION FROM ANDERSON - NOAA TECH REPORT NWS 19
C ***  BASED ON DATA FROM ALTA, UTAH
C-----------------------------------------------------------------------
C
C  CALCULATE NEW SNOW DENSITY BASED ON TEMPERATURE
      IF(TSNEW.LE.-15.) THEN
        DENNEW=0.05 
      ELSE                                                      
        DENNEW=0.05+0.0017*(TSNEW+15.)**1.5
      ENDIF
C
C  CALCULATE DEPTH OF NEW SNOWFALL      
      DPTNEW=(0.1*SFALL)/DENNEW
      RETURN
      END      
C      
      SUBROUTINE SNOWPACK (WE,DT,SNDPT,SNDEN,SLIQ,SNTMP,IBUG)
C
C ****************************************************************
C **  SUBROUTINE CALCULATES SNOW COMPACTION AND DESTRUCTIVE    ***
C **  METAMORPHISM - ALSO COMPONENT OF MELT METAMORPHISM DUE   ***
C **  TO THE PRESENCE OF LIQUID WATER IS ACCOUNTED FOR.        ***
C **  EQUATIONS FOR INCREASING SNOW DENSITY WERE OBTAINED AS   ***
C **  APPROXIMATE SOLUTIONS OF E. ANDERSON DIFFERENTIAL        ***
C **  EQUATIONS (3.29) AND (3.30), (3.31), NOAA TECHNICAL      *** 
C **  REPORT NWS 19, by   VICTOR KOREN   03/25/95              ***
C **                                                           ***
C **  MODIFIED 11/05 BY E. ANDERSON -                          ***
C **    SOME VARIABLE NAMES CHANGED TO PROVIDE CONSISTENCY     ***
C **    EFFECT OF SNOWFALL, MELT, AND REFREEZE ON DEPTH NOW    ***
C **    MOVED UP TO CALLING ROUTINE - SNDEPTH19                ***
C ****************************************************************
C
C ***************************************************************
C  WE     - WATER EQUIVALENT OF THE ICE PORTION OF THE        ***
C            REMAINING PART OF THE SNOW COVER THAT EXISTED    ***
C            AT THE START OF THE COMPUTATIONAL PERIOD, MM     ***
C  DT     - LENGTH OF COMPUTATIONAL TIME PERIOD, HRS          ***
C  SNDPT  - SNOW DEPTH, CM                                    ***
C  SNDEN  - SNOW DENSITY, G/CM3                               ***
C  SLIQ   - LIQUID WATER CONTENTS, MM (END OF PERIOD)         ***
C  SNTMP  - AVERAGE SNOW TEMPERATURE OF REMAINING EXISTING    ***
C             SNOW COVER, C (END OF PERIOD)                   ***
C                                                             ***
C   INPUT -SNDPT AND SNDEN AT BEGINNING OF PERIOD             ***
C   OUTPUT - SNDPT AND SNDEN - VALUES FOR REMAINING EXISTING  ***
C     SNOW AFTER COMPACTION AND DESTRUCTIVE METAMORPHISM      ***
C ***************************************************************
C
      REAL*8 B
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C  CONSTANTS OF SNOW COMPACTION AND DESTRUCTIVE METAMORPHISM
C    MODELS FROM ANDERSON - TECHNICAL REPORT 19:
C    C1 IN 1/(CM HR), C2 IN CM3/G, C3 IN 1/HR, C4 IN 1/DEGREE C,
C    C5 DIMENSIONLESS, THRESD IN G/CM3, CX IN CM3/G
C    ONLY PARAMETER C5 WAS CHANGED FROM 0.15 TO 0.20 BASED ON
C    RESULTS OF SIMULATIONS USING NOAA-ARS COOPERATIVE SNOW 
C    RESEARCH PROJECT DATA 
CEA  BASED ON COMPARISON AT ALL SNOWMIP LOCATIONS AND SEVERAL
CEA    OTHER SITES THE SNOW METAMORPHISM PARAMETERS WERE FURTHER
CEA    MODIFIED TO PRODUCE IMPROVED RESULTS OVER A WIDER RANGE OF
CEA    CLIMATOLOGICAL CONDITIONS.
      PARAMETER (C1=0.026, C2=21.0)
      PARAMETER (C3=0.005, C4=0.10, THRESD=0.15, C5=2.,CX=23.)
C
C **  CONVERT WATER EQUIVALENT TO CENTIMETERS
      SNDPT1=SNDPT
      SNDEN1=SNDEN
      WECM=WE*0.1
C
C **  CALCULATE CHANGE IN DENSITY AS A RESULT OF COMPACTION
C **  C1 IS THE FRACTIONAL INCREASE IN DENSITY (1/(CM*HR)) 
C **  C2 IS A CONSTANT (CM3/G) Kojima estimated as 21 cms/g
C   
      DCC=1.
      IF(WECM.GT.1E-2) THEN
        B=DT*C1*EXP(0.08*SNTMP-C2*SNDEN)
        DCC=(DEXP(B*WECM)-1.)/(B*WECM)
      ENDIF
C
C **  CALCULATE THE DENSITY CHANGE AS A RESULT OF DESTRUCTIVE
C       METAMORPHISM AND COMPONENT OF MELT METAPHORISM DUE TO
C       THE PRESENCE OF LIQUID WATER
C     C3 IS THE FRACTIONAL SETTLING RATE AT 0 DEGREE FOR DENSITIES 
C        LESS THAN THRESHOLD DENSITY, THRESD
C     C4 IS A CONSTANT 
C     C5 IS AN EMPIRICAL ADJUSTMENT FACTOR THAT ACCOUNTS FOR
C        THE PRESENCE OF LIQUID WATER
C 
      A1=C3
      IF(SLIQ .GT. 0.) A1=A1*C5
      A2=C4*SNTMP
      IF(SNDEN.GT.THRESD) A2=A2-CX*(SNDEN-THRESD)
      A=A1*DT*EXP(A2)
      DCD=EXP(A)

C **  NEW SNOW DENSITY OF EXISTING SNOW AS A RESULT OF COMPACTION
C **   AND DESTRUCTIVE METAMORPHISM 
      SNDEN=SNDEN*DCC*DCD
C **  NEW DEPTH OF EXISTING SNOW AFTER DENSITY CHANGE
      SNDPT=WECM/SNDEN
      IF (IBUG.EQ.1) WRITE(IODBUG,900) WE,SNDPT1,SNDEN1,SNTMP,SLIQ,
     1  SNDPT,SNDEN,DCC,DCD
  900 FORMAT(1H ,2X,20HSNOWPACK INPUT - WE=,F8.2,7H SNDPT=,F7.2,
     1  7H SNDEN=,F5.3,7H SNTMP=,F5.1,6H SLIQ=,F5.2,/7X,
     2  15HOUTPUT - SNDPT=,F7.2,7H SNDEN=,F5.3,5H DCC=,F7.5,
     3  5H DCD=,F7.5)
C********************************************************************
CEA  PREVIOUS VERSION OF THE ROUTINE AS FOLLOWS
CEA      SUBROUTINE SNOWPACK ( W,DTS,HC,DS,SLIQ,DFALL,SRFRZ,TSNOW)
C
C **************************************************************
C  W      IS A WATER EQUIVALENT OF SNOW, IN MM               ***
C  DTS    IS A TIME STEP, IN HOURS                           ***
C  HC     IS A SNOW DEPTH, IN CM                              ***
C  DS     IS A SNOW DENSITY, IN G/CM3                        ***
C  SLIQ   IS A LIQUID WATER CONTENTS, MM                     ***
C  TSNOW  IS AN AVERAGE SNOW TEMPERATURE, CELSIUS            ***
C      SUBROUTINE WILL RETURN NEW VALUES OF H AND DS         ***
C **************************************************************
C
CEA      REAL*8 B
C
C  CONSTANTS OF SNOW COMPACTION/METAMORPHISM MODELS. FROM ANDERSON
C  TECHNICAL REPORT 19:
C    C1 IN 1/(CM HR), C2 IN CM3/G, C3 IN 1/HR, C4 IN 1/DEGREE C,
C    C5 DIMENSIONLESS, RDS IN G/CM3, CX IN CM3/G
C    ONLY PARAMETER C5 WAS CHANGED FROM 0.15 TO 0.20 BASED ON
C    RESULTS OF SIMULATIONS USING NOAA-ARS COOPERATIVE SNOW 
C    RESEARCH PROJECT DATA 
CEA      PARAMETER (C1=0.01, C2=21.0)
CEA      PARAMETER (C3=0.01, C4=0.04, RDS=0.20, C5=2.,CX=46.)
C
C **  CONVERSION INTO SIMULATION UNITS
CEA      WX=W*0.1
C
C **  CALCULATING OF SNOW DEPTH AND DENSITY AS A RESULT OF COMPACTION
C **  C1 IS THE FRACTIONAL INCREASE IN DENSITY (1/(CM*HR)) 
C **  C2 IS A CONSTANT (CM3/G) Kojima estimated as 21 cms/g
C   
CEA     DSC=1.
CEA     IF(WX.GT.1E-2) THEN
CEA        B=DTS*C1*EXP(0.08*TSNOW-C2*DS)
CEA        DSC=(DEXP(B*WX)-1.)/(B*WX)
CEA      ENDIF
C
C **  CALCULATE THE DENSITY CHANGE AS A RESULT OF SNOW METAMORPHISM
C     C3 IS THE FRACTIONAL SETTLING RATE AT 0 DEGREE FOR DENSITIES 
C        LESS THAN THRESHOLD DENSITY, RDS
C     C4 IS A CONSTANT 
C     C5 IS AN EMPIRICAL ADJUSTMENT FACTOR THAT ACCOUNTS FOR MELT
C        METAMORPHISM
C 
CEA      A=C3
CEA      IF(SLIQ .GT. 0.) A=A*C5
CEA      C=C4*TSNOW
CEA      IF(DS .GT. RDS) C=C-CX*(DS-RDS)
CEA      DSM=EXP(A*DTS*EXP(C))
C **  NEW SNOW DENSITY AS A RESULT OF COMPACTION-METAMORPHISM 
CEA      DSX=DS*DSC*DSM
CEA      IF(DSX .GT. 0.45) DSX=0.45
CEA      IF(DSX .LT. 0.05) DSX=DS
CEA      DS=DSX
CEA      DWX=WX-0.1*(DFALL+SRFRZ)
CEA      IF(DWX .GT. 0.) THEN
CEA       HC=DWX/DS
CEA      ELSE 
CEA       IF(WX .GT. 0.) THEN
CEA        HC=WX/DSX
CEA       ELSE
CEA        HC=0.0
CEA       ENDIF  
CEA      ENDIF
C********************************************************************
      RETURN
      END